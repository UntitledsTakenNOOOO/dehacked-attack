ACTOR RowdyRudyClass : DEHBoolean {}
ACTOR RowdyRudy : DoomPlayer
{
  Player.DisplayName "Rowdy Rudy (Rowdy Rudy 2)"
  Player.StartItem "RowdyRudyRifle"
  Player.StartItem "RowdyRudyFists"
  Player.StartItem "Clip", 150
  Player.StartItem "RowdyRudyClass"
/*
  Player.WeaponSlot 1, RowdyRudyChainsaw, RowdyRudyFists
  Player.WeaponSlot 2, RowdyRudyRifle
  Player.WeaponSlot 3, RowdyRudySuperShotgun, RowdyRudyShotgun
  Player.WeaponSlot 4, RowdyRudyChaingun
  Player.WeaponSlot 5, RowdyRudyRocketLauncher
  Player.WeaponSlot 6, RowdyRudyPlasmaRifle
  Player.WeaponSlot 7, RowdyRudyBFG9000
*/
  States
  {
  Spawn:
    BLAY A -1
    Loop
  See:
    BLAY ABCD 4 
    Loop
  Missile:
    BLAY E 12
    Goto Spawn
  Melee:
    BLAY F 6 BRIGHT
    Goto Missile
  Pain:
    BLAY G 4 
    BLAY G 4 A_Pain
    Goto Spawn
  Death:
    BLAY H 10
    BLAY I 10 A_PlayerScream
    BLAY J 10 A_NoBlocking
    BLAY KLM 10
    BLAY N -1
    Stop
  XDeath:
    BLAY O 5
    BLAY P 5 A_XScream
    BLAY Q 5 A_NoBlocking
    BLAY RSTUV 5
    BLAY W -1
    Stop
  }
}

ACTOR RowdyRudyClip : Clip
{
  States
  {
  Spawn:
    KLIP A -1
    Stop
  }
}
ACTOR RowdyRudyClipBox : Clip
{
  Inventory.Amount 50
  States
  {
  Spawn:
    BMMO A -1
    Stop
  }
}
ACTOR RowdyRudyRocketBox : RocketAmmo
{
  Inventory.Amount 5
  States
  {
  Spawn:
    CROK A -1
    Stop
  }
}

ACTOR RowdyRudyFists : DoomWeapon
{
  Weapon.SlotNumber 1
  Weapon.SelectionOrder 3700
  Weapon.Kickback 100
  Obituary "$OB_MPFIST"
  Tag "Fists"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    PUNC A 1 A_WeaponReady
    Loop
  Deselect:
    PUNC AAA 0 A_Lower
    PUNC A 1 A_Lower
    Loop
  Select:
    PUNC AAA 0 A_Raise
    PUNC A 1 A_Raise
    Loop
  Fire:
    PUNC B 1 
    PUNC CD 1 A_Punch
    PUNC D 1 
    PUNC CBA 2 
    PUNC EE 1 A_Punch
    PUNC F 3 
    PUNC E 2 
    PUNC A 2 A_ReFire
    Goto Ready
  }
}

ACTOR RowdyRudyChainsaw : DoomWeapon
{
  Weapon.SlotNumber 1
  Weapon.Kickback 0
  Weapon.SelectionOrder 2200
  Weapon.UpSound "weapons/sawup"
  Weapon.ReadySound "weapons/sawidle"
  Inventory.PickupMessage "$GOTCHAINSAW"
  Obituary "$OB_MPCHAINSAW"
  Tag "$TAG_CHAINSAW"
  +WEAPON.MELEEWEAPON
  States
  {
  Ready:
    SAWW CD 4 A_WeaponReady
    Loop
  Deselect:
    SAWW CCC 0 A_Lower
    SAWW C 1 A_Lower
    Loop
  Select:
    SAWW CCC 0 A_Raise
    SAWW C 1 A_Raise
    Loop
  Fire:
    SAWW AB 3 A_Saw
    SAWW B 0 A_ReFire
    Goto Ready
  Spawn:
    CSAW A -1
    Stop
  }
}

ACTOR RowdyRudyRifle : DoomWeapon
{
  Weapon.SlotNumber 2
  Weapon.SelectionOrder 1900
  Weapon.AmmoUse 1
  Weapon.AmmoGive 20
  Weapon.AmmoType "Clip"
  Obituary "%o was gunned down by %k."
  +WEAPON.WIMPY_WEAPON
  Inventory.Pickupmessage "You got a rifle!"
  Tag "Rifle"
  States
  {
  Ready:
    RRRI A 1 A_WeaponReady
    Loop
  Deselect:
    RRRI AAA 0 A_Lower
    RRRI A 1 A_Lower
    Loop
  Select:
    RRRI AAA 0 A_Raise
    RRRI A 1 A_Raise
    Loop
  Fire:
    RRRI A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BalancedMode") == 1, "BalancedFire")
    RRRI A 0
    RRRI B 0 A_FireBullets(0, 0, 1, 5, "BulletPuff")
    RRRI B 0 A_PlaySound("rowdyrudy/pistol", CHAN_WEAPON)
    RRRI B 3 A_GunFlash
    RRRI A 2
    Goto Ready
  BalancedFire:
    RRRI A 0
    RRRI B 0 A_GunFlash("BalancedFlash")
    RRRI B 0 A_PlaySound("rowdyrudy/pistol", CHAN_WEAPON)
    RRRI B 2 A_FireBullets(0, 0, 1, 5, "BulletPuff")
    RRRI B 0 A_PlaySound("rowdyrudy/pistol", CHAN_WEAPON)
    RRRI B 2 A_FireBullets(0, 0, 1, 5, "BulletPuff")
    RRRI CDCBA 2
    Goto Ready
  Flash:
    RRRF A 2 Bright A_Light1
    Goto LightDone
  BalancedFlash:
    RRRF A 1 Bright A_Light1
    TNT1 A 1 Bright A_Light0
    RRRF A 1 Bright A_Light1
    Goto LightDone
  Spawn:
    PIST A -1
    Stop
  }
}

ACTOR RowdyRudyShotgun : DoomWeapon
{
  Weapon.SlotNumber 3
  Weapon.SelectionOrder 1300
  Weapon.AmmoUse 1
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN"
  Obituary "$OB_MPSHOTGUN"
  Tag "$TAG_SHOTGUN"
  States
  {
  Ready:
    SHT1 A 1 A_WeaponReady
    Loop
  Deselect:
    SHT1 AAA 0 A_Lower
    SHT1 A 1 A_Lower
    Loop
  Select:
    SHT1 AAA 0 A_Raise
    SHT1 A 1 A_Raise
    Loop
  Fire:
    SHT1 A 2
    SHT1 A 0 A_FireBullets(5.6, 0, 7, 5, "BulletPuff")
    SHT1 A 0 A_PlaySound("rowdyrudy/shotgf", CHAN_WEAPON)
    SHT1 A 4 A_GunFlash
    SHT1 B 1
    SHT1 CDC 2
    SHT1 BA 3
    SHT1 C 2
    SHT1 A 3 A_ReFire
    Goto Ready
  Flash:
    SHTA A 2 Bright A_Light1
    SHTA B 2 Bright A_Light2
    Goto LightDone
  Spawn:
    SGUN A -1
    Stop
  }
}

ACTOR RowdyRudySuperShotgun : DoomWeapon
{
  Weapon.SlotNumber 3
  Weapon.SelectionOrder 400
  Weapon.AmmoUse 2
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN2"
  Obituary "$OB_MPSSHOTGUN"
  Tag "$TAG_SUPERSHOTGUN"
  States
  {
  Ready:
    SHH2 A 1 A_WeaponReady
    Loop
  Deselect:
    SHH2 AAA 0 A_Lower
    SHH2 A 1 A_Lower
    Loop
  Select:
    SHH2 AAA 0 A_Raise
    SHH2 A 1 A_Raise
    Loop
  Fire:
    SHH2 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BalancedMode") == 1, "BalancedFire")
    SHH2 A 3 
    SHH2 A 0 A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff")
    SHH2 A 0 A_PlaySound("rowdyrudy/sshotgf", CHAN_WEAPON)
    SHH2 A 4 A_GunFlash
    SHH2 B 3 
    SHH2 C 4 //A_CheckReload
    SHH2 D 5 //A_PlaySound("rowdyrudy/sshotgo", CHAN_WEAPON)
    SHH2 E 6 
    SHH2 F 6 //A_PlaySound("rowdyrudy/sshotgl", CHAN_WEAPON)
    SHH2 G 5 
    SHH2 H 4 //A_PlaySound("rowdyrudy/sshotgc", CHAN_WEAPON)
    SHH2 A 4 A_ReFire
    goto Ready
  BalancedFire:
    SHO2 A 3
    SHH2 A 0 A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff")
    SHH2 A 0 A_PlaySound("rowdyrudy/sshotgf", CHAN_WEAPON)
    SHH2 A 7 A_GunFlash("BalancedFlash")
    SHO2 B 7
    SHO2 C 7 A_CheckReload
    SHO2 D 7 A_OpenShotgun2
    SHO2 E 7
    SHO2 F 7 A_LoadShotgun2
    SHO2 G 6
    SHO2 H 6 A_CloseShotgun2
    SHO2 A 5 A_ReFire
    Goto Ready
  Flash:
    SHH2 I 2 Bright A_Light1
    SHH2 J 2 Bright A_Light2
    Goto LightDone
  BalancedFlash:
    SHO2 I 4 Bright A_Light1
    SHO2 J 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SGN2 A -1
    Stop
  }
}

ACTOR RowdyRudyChaingun : DoomWeapon
{
   Weapon.SlotNumber 4
   Weapon.SelectionOrder 700
   Weapon.AmmoUse 1
   Weapon.AmmoGive 20
   Weapon.AmmoType "Clip"
   Inventory.PickupMessage "$GOTCHAINGUN" // "You got the chaingun"
   Obituary "$OB_MPCHAINGUN" // "%o was mowed down by %k's chaingun."
   Tag "$TAG_CHAINGUN"
   States
   {
   Ready:
     CHAI A 1 A_WeaponReady
     Loop
   Deselect:
     CHAI AAA 0 A_Lower
     CHAI A 1 A_Lower
     Loop
   Select:
     CHAI AAA 0 A_Raise
     CHAI A 1 A_Raise
     Loop
  Fire:
     CHAI A 0 A_PlaySound("rowdyrudy/chngun", CHAN_WEAPON)
     CHAI A 0 A_GunFlash
     CHAI A 3 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
     CHAI B 0 A_PlaySound("rowdyrudy/chngun", CHAN_WEAPON)
     CHAI B 0 A_GunFlash("Flash2")
     CHAI B 3 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
     CHAI B 0 A_ReFire
     Goto Ready
  Flash:
     CHIF A 4 Bright A_Light1
     Goto LightDone
  Flash2:
     CHIF B 4 Bright A_Light2
     Goto LightDone
   Spawn:
     CGUN A -1
     Stop
   }
}

ACTOR RowdyRudyRocketLauncher : DoomWeapon
{
  Weapon.SlotNumber 5
  Weapon.SelectionOrder 2500
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "$GOTLAUNCHER"
  Tag "$TAG_ROCKETLAUNCHER"
  States
  {
  Ready:
    RISG A 1 A_WeaponReady
    Loop
  Deselect:
    RISG AAA 0 A_Lower
    RISG A 1 A_Lower
    Loop
  Select:
    RISG AAA 0 A_Raise
    RISG A 1 A_Raise
    Loop
  Fire:
    RISG A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BalancedMode") == 1, "BalancedFire")
    RISG B 8 A_GunFlash
    RISG B 9 A_FireCustomMissile("RowdyRudyRocket", 0, 1)
    RISG A 0 A_Refire
    Goto Ready
  BalancedFire:
    RISG B 8 A_GunFlash("BalancedFlash")
    RISG B 11 A_FireCustomMissile("RowdyRudyRocket", 0, 1)
    RISG A 0 A_Refire
    Goto Ready
  Flash:
    RISF A 1 Bright A_Light1
    RISF B 2 Bright A_Light1
    RISF CD 3 Bright A_Light2
    Goto LightDone
  BalancedFlash:
    RISF A 3 Bright A_Light1
    RISF B 4 Bright A_Light1
    RISF CD 4 Bright A_Light2
    Goto LightDone
  Spawn:
    RAUN A -1
    Stop
  }
}
ACTOR RowdyRudyRocket : Rocket
{
  SeeSound "rowdyrudy/rocklf"
}
/*
ACTOR RowdyRudyPlasmaRifle : DoomWeapon
{
  Weapon.SlotNumber 6
  Weapon.SelectionOrder 100
  Weapon.AmmoUse 1
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Inventory.PickupMessage "$GOTPLASMA"
  Tag "$TAG_PLASMARIFLE"
  States
  {
  Ready:
    PLSG A 1 A_WeaponReady
    Loop
  Deselect:
    PLSG A 1 A_Lower
    Loop
  Select:
    PLSG A 1 A_Raise
    Loop
  Fire:
    PLSG A 0 A_Jump(128,"Fire2")
    PLSG A 0 A_GunFlash
    PLSG A 3 A_FireCustomMissile("RowdyRudyPlasmaBall", 0, 1)
    PLSG BBBBBBBBBBBBBBBBBBBB 1 A_ReFire
    Goto Ready
  Fire2:
    PLSG A 0 A_GunFlash("Flash2")
    PLSG A 3 A_FireCustomMissile("RowdyRudyPlasmaBall", 0, 1)
    PLSG BBBBBBBBBBBBBBBBBBBB 1 A_ReFire
    Goto Ready
  Flash:
    XLSF A 4 Bright A_Light1
    Goto LightDone
  Flash2:
    XLSF B 4 Bright A_Light1
    Goto LightDone
  Spawn:
    PLAS A -1
    Stop
  }
}
ACTOR RowdyRudyPlasmaBall : PlasmaBall
{
  SeeSound "rowdyrudy/plasmaf"
  States
  {
  Spawn:
    APLS AB 5 Bright
    Loop
  Death:
    APLX ABCDE 5 Bright
    Stop
  }
}
*/
ACTOR RowdyRudyBFG9000 : DoomWeapon
{
  Height 20
  Weapon.SlotNumber 7
  Weapon.SelectionOrder 2800
  Weapon.AmmoUse 40
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "$GOTBFG9000"
  Tag "$TAG_BFG9000"
  States
  {
  Ready:
    BFGR A 1 A_WeaponReady
    Loop
  Deselect:
    BFGR AAA 0 A_Lower
    BFGR A 1 A_Lower
    Loop
  Select:
    BFGR AAA 0 A_Raise
    BFGR A 1 A_Raise
    Loop
  Fire:
    BFGR A 20 A_PlaySound("rowdyrudy/bfgf", CHAN_WEAPON)
	BFGR B 12 A_GunFlash
    BFGR C 8 A_FireCustomMissile("RowdyRudyBFGBall", 0, 1)
    BFGR A 1 A_Refire
    Goto Ready
  Flash:
    BFNO A 11 Bright A_Light1
    BFNO B 6 Bright A_Light2
    Goto LightDone
  Spawn:
    BFUG A -1
    Stop
  }
}
ACTOR RowdyRudyBFGBall : BFGBall
{
  DeathSound "rowdyrudy/bfgx"
  States
  {
  Spawn:
    BFB1 AB 4 Bright
    Loop
  Death:
    BFX1 AB 4 Bright
    BFX1 C 4 Bright A_BFGSpray("RowdyRudyBFGExtra")
    BFX1 DEF 4 Bright
    Stop
  }
}
ACTOR RowdyRudyBFGExtra : BFGExtra
{
  States
  {
  Spawn:
    FIRE ABCDEF 4 Bright
    FIRE G 3 Bright
    FIRE H 2 Bright
    Stop
  }
}