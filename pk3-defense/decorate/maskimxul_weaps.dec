ACTOR MaskimXulClass : DEHBoolean {}
ACTOR JohnMaskimXul : DoomPlayer
{
  Player.DisplayName "Doomguy (Maskim Xul)"
  Player.StartItem "DEHFist"
  Player.StartItem "DualPistols"
  Player.StartItem "Clip", 10
  Player.StartItem "MaskimXulClass"
/*
  Player.WeaponSlot 1, Chainsaw, Fist
  Player.WeaponSlot 2, DualPistols
  Player.WeaponSlot 3, SuperShotgun, Shotgun
  Player.WeaponSlot 4, Chaingun
  Player.WeaponSlot 5, MaskimXulHandOfAfrit
  Player.WeaponSlot 6, MaskimXulJackBombs
  Player.WeaponSlot 7, MaskimXulGrimoireExcidium
*/
}

//The next five actors are all a gross hack.
ACTOR MaskimXulRouletteSoulSphere : SoulSphere
{
  +INVENTORY.BIGPOWERUP
  States
  {
  Spawn:
    SOUL A 0
	SOUL A 6 Bright
	SOUL A 0 A_ChangeFlag("COUNTITEM", 0)
	SOUL A 0 A_SpawnItemEx("MaskimXulRouletteInvulnerabilitySphere", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}
ACTOR MaskimXulRouletteInvulnerabilitySphere : InvulnerabilitySphere
{
  States
  {
  Spawn:
    PINV E 0
	PINV E 6 Bright
	PINV E 0 A_ChangeFlag("COUNTITEM", 0)
	PINV E 0 A_SpawnItemEx("MaskimXulRouletteBlurSphere", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}
ACTOR MaskimXulRouletteBlurSphere : BlurSphere
{
  States
  {
  Spawn:
    PINS E 0
	PINS E 6 Bright
	PINS E 0 A_ChangeFlag("COUNTITEM", 0)
	PINS E 0 A_SpawnItemEx("MaskimXulRouletteMegaSphere", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}
ACTOR MaskimXulRouletteMegaSphere : MegaSphere
{
  +INVENTORY.BIGPOWERUP
  States
  {
  Spawn:
    MEGA E 0
	MEGA E 6 Bright
	MEGA E 0 A_ChangeFlag("COUNTITEM", 0)
	MEGA E 0 A_SpawnItemEx("MaskimXulRouletteBerserkSphere", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}
ACTOR MaskimXulRouletteBerserkSphere : Berserk
{
  +INVENTORY.BIGPOWERUP
  States
  {
  Spawn:
    PSTR E 0
	PSTR E 6 Bright
	PSTR E 0 A_ChangeFlag("COUNTITEM", 0)
	PSTR E 0 A_SpawnItemEx("MaskimXulRouletteSoulSphere", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
    Stop
  }
}

ACTOR MaskimXulHandOfAfrit : DoomWeapon
{
  Weapon.SlotNumber 5
  Weapon.SelectionOrder 2500
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "You got the Hand of Afrit!"
  Tag "Hand of Afrit"
  States
  {
  Ready:
    MXRL A 1 A_WeaponReady
    Loop
  Deselect:
    MXRL A 1 A_Lower
    Loop
  Select:
    MXRL A 1 A_Raise
    Loop
  Fire:
    MXRL A 0 A_JumpIfInventory("MaskimXulClass", 1, "FireMaskimXul")
    MXRL B 4 Bright A_GunFlash
    MXRL C 0 Bright A_FireCustomMissile("MaskimXulHandOfAfritComet", 0, 1)
    MXRL C 8 Bright A_TakeInventory("RocketAmmo", 1)
    MXRL D 4
    MXRL E 4
    MXRL A 0 A_ReFire
    Goto Ready
  FireMaskimXul:
    MXRL B 4 Bright A_GunFlash
    MXRL C 8 Bright A_FireCustomMissile("MaskimXulHandOfAfritComet", 0, 1)
    MXRL D 4
    MXRL E 4
    MXRL A 0 A_ReFire
    Goto Ready
  Flash:
    TNT1 A 4 Bright A_Light1
    TNT1 AA 4 Bright A_Light2
    Goto LightDone
  Spawn:
    MXLN ABCDCB 4
    Loop
  }
}
ACTOR MaskimXulHandOfAfritComet : Rocket
{
  SeeSound "maskimxul/rocklf"
  Damage 24
  States
  {
  Spawn:
    COMT ABC 4 Bright
    Loop
  Death:
    COMT D 0 Bright A_Explode
    COMT D 3 Bright A_Explode
    COMT EFGHI 3 Bright
    Stop
  }
}

ACTOR MaskimXulDemonHeart : RocketAmmo
{
  Inventory.PickupMessage "Picked up a demon heart."
  Inventory.Amount 1
  //Inventory.MaxAmount 50
  //Ammo.BackpackAmount 1
  //Ammo.BackpackMaxAmount 100
  //Inventory.Icon "MXROA0"
  States
  {
  Spawn:
    MXRO ABCD 4
    Loop
  }
}
ACTOR MaskimXulDemonSoul : RocketAmmo
{
  Inventory.PickupMessage "Picked up a demon soul."
  Inventory.Amount 5
  States
  {
  Spawn:
    MXRB ABCD 4
    Loop
  }
}

ACTOR MaskimXulJackBombs : DoomWeapon
{
  Weapon.SlotNumber 6
  Weapon.SelectionOrder 100
  Weapon.AmmoUse 1
  Weapon.AmmoGive 40
  Weapon.AmmoType "JackBombAmmo"
  Inventory.PickupMessage "Picked up the Jackbomb thrower."
  Tag "Jackbombs"
  States
  {
  Ready:
    MXPR A 1 A_WeaponReady
    Loop
  Deselect:
    MXPR A 1 A_Lower
    Loop
  Select:
    MXPR A 1 A_Raise
    Loop
  Fire:
    MXPR A 3
    TNT1 A 15 A_FireCustomMissile("JackBomb", 0, 1)
    MXPR EDCB 3
    MXPR A 3 A_Refire
    Goto Ready
  Spawn:
    MXPA A -1
    Stop
  }
}
ACTOR JackBombAmmo : Ammo
{
  Inventory.PickupMessage "Picked up a jack bomb."
  Inventory.Amount 1
  Inventory.MaxAmount 10
  Ammo.BackpackAmount 1
  Ammo.BackpackMaxAmount 20
  Inventory.Icon "MXCLA0"
  States
  {
  Spawn:
    MXCL A -1
    Stop
  }
}
ACTOR JackBomb : PlasmaBall
{
  -NOGRAVITY
  Speed 8
  Damage 1
  SeeSound None
  PainSound "weapons/rocklx"
  DeathSound "maskimxul/jckbx1"
  RenderStyle "Normal"
  States
  {
  Spawn:
  MXPL A 3 BRIGHT
  Loop
  Death:
  MXPL ABCBAB 21 BRIGHT
  MXPL C 14 BRIGHT
  MXPL D 3 BRIGHT A_PlaySound("maskimxul/jckbx2", CHAN_BODY)
  MXPL E 3 BRIGHT
  MXPL F 15 BRIGHT
  MISL BBBBBBBB 0 BRIGHT A_Explode
  MISL B 8 BRIGHT A_Pain
  MISL C 6 BRIGHT
  MISL D 4 BRIGHT
  Stop
  }
}

ACTOR MaskimXulGrimoireExcidium : DoomWeapon
{
  Height 20
  Weapon.SlotNumber 7
  Weapon.SelectionOrder 2800
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "You got the Grimoire Excidium! Use it wisely..."
  Tag "Grimoire Excidium"
  States
  {
  Ready:
    MXBG A 1 A_WeaponReady
    Loop
  Deselect:
    MXBG A 1 A_Lower
    Loop
  Select:
    MXBG A 1 A_Raise
    Loop
  Fire:
    MXBG B 10 A_PlaySound("maskimxul/bfgf", CHAN_WEAPON)
    MXBG C 5 A_GunFlash
    MXBG D 5 Bright
    MXBG E 5 Bright A_FireCustomMissile("MaskimXulGrimoireExcidiumAttack", 0, 0)
	MXBG CB 5
    Goto Ready
  Flash:
    TNT1 A 5 Bright A_Light1
    TNT1 A 10 Bright A_Light2
    TNT1 A 5 Bright A_Light1
    Goto LightDone
  Spawn:
    MXBF A -1
    Stop
  }
}
ACTOR MaskimXulGrimoireExcidiumAttack : BFGBall
{
  DeathSound ""
  States
  {
  Spawn:
    TNT1 A 0
	TNT1 A 0 A_Die
    Goto Death
  Death:
    TNT1 A 0 Bright
    TNT1 A 0 Bright A_BFGSpray("MaskimXulBFGExtra")
    TNT1 A 0 Bright A_BFGSpray("MaskimXulBFGExtra")
    TNT1 A 0 Bright A_BFGSpray("MaskimXulBFGExtra")
    TNT1 A 0 Bright A_SargAttack
    TNT1 A 1 Bright A_SargAttack
    Stop
  }
}
ACTOR MaskimXulBFGExtra : BFGExtra
{
	Translation 0
}
