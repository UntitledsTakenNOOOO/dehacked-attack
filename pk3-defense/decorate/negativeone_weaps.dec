ACTOR NegativeOneClass : DEHBoolean {}
ACTOR JohnNegativeOne : DoomPlayer
{
  Player.DisplayName "Son of Doomguy (Doom -1)"
  Player.StartItem "NegativeOnePistol"
  Player.StartItem "DEHFist"
  Player.StartItem "Clip", 50
  Player.StartItem "NegativeOneClass"
  Player.SoundClass "NegativeOne"
/*
  Player.WeaponSlot 1, NegativeOneGauntlets, Fist
  Player.WeaponSlot 2, NegativeOnePistol
  Player.WeaponSlot 3, NegativeOneSSG, NegativeOneShotgun
  Player.WeaponSlot 4, NegativeOneUzi
  Player.WeaponSlot 5, NegativeOneRocketLauncher
  Player.WeaponSlot 6, NegativeOneDarkSceptre
  Player.WeaponSlot 7, NegativeOneUnmakyr
*/
  States
  {
  Spawn:
    NPLY A -1
    Loop
  See:
    NPLY ABCD 4 
    Loop
  Missile:
    NPLY E 12
    Goto Spawn
  Melee:
    NPLY F 6 BRIGHT
    Goto Missile
  Pain:
    NPLY G 4 
    NPLY G 4 A_Pain
    Goto Spawn
  Death:
    NPLY H 10
    NPLY I 10 A_PlayerScream
    NPLY J 10 A_NoBlocking
    NPLY KLM 10
    NPLY N -1
    Stop
  XDeath:
    NPLY O 5
    NPLY P 5 A_XScream
    NPLY Q 5 A_NoBlocking
    NPLY RSTUV 5
    NPLY W -1
    Stop
  }
}

ACTOR NegativeOneClip : Clip
{
  Inventory.PickupSound "negativeone/i_pkup"
  States
  {
  Spawn:
    NGCL A -1
    Stop
  }
}
ACTOR NegativeOneStimpack : Health
{
  Inventory.Amount 10
  Inventory.PickupMessage "$GOTSTIM" // "Picked up a stimpack."
  States
  {
  Spawn:
    NSTM A -1
    Stop
  }
}
ACTOR NegativeOneMedikit : Health
{
  Inventory.Amount 25
  Inventory.PickupMessage "$GOTMEDIKIT" // "Picked up a medikit."
  Health.LowMessage 25, "$GOTMEDINEED" // "Picked up a medikit that you REALLY need!"
  States
  {
  Spawn:
    NMED A -1
    Stop
  }
}
ACTOR NegativeOneGreenArmor : BasicArmorPickup
{
  Radius 20
  Height 16
  Inventory.PickupMessage "$GOTARMOR" // "Picked up the armor."
  Inventory.Icon "NGA1A0"
  Armor.SavePercent 33.335
  Armor.SaveAmount 100
  Inventory.PickupSound "negativeone/i_pkup"
  States
  {
  Spawn:
    NGA1 A 6
    NGA1 B 6 Bright
    Loop
  }
}
ACTOR NegativeOneBlueArmor : BasicArmorPickup
{
  Radius 20
  Height 16
  Inventory.PickupMessage "$GOTMEGA" // "Picked up the MegaArmor!"
  Inventory.Icon "NGA2A0"
  Armor.SavePercent 50
  Armor.SaveAmount 200
  Inventory.PickupSound "negativeone/i_pkup"
  States
  {
  Spawn:
    NGA2 A 6
    NGA2 B 6 Bright
    Loop
  }
}

ACTOR NegativeOneGauntlets : Weapon
{
  Weapon.SlotNumber 1
  Weapon.Kickback 0
  Weapon.SelectionOrder 2200
  Weapon.UpSound "negativeone/sawup"
  Weapon.ReadySound "negativeone/sawidle"
  Inventory.PickupMessage "You got the Gauntlets of Power, electrifying!"
  Obituary "%o was smitten by %k's lightning."
  Tag "Gauntlets of Power"
  +WEAPON.MELEEWEAPON
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NGSW CCCCDDDD 1 A_WeaponReady
    Loop
  Deselect:
    NGSW C 1 A_Lower
    Loop
  Select:
    NGSW C 1 A_Raise
    Loop
  Fire:
    NGSW A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BalancedMode") == 0, "FirePowered") //?!
    NGSW AB 4 A_Saw("negativeone/sawfull", "negativeone/sawhit", 2, "NegativeOnePuff", SF_RANDOMLIGHTBOTH, 64)
    NGSW B 0 A_ReFire
    Goto Ready
  FirePowered:
    NGSW AB 4 A_Saw("negativeone/sawfull", "negativeone/sawhit", 2, "NegativeOnePuff", SF_RANDOMLIGHTBOTH, 256, 2.8125, 0, 0.5, 0)
    NGSW B 0 A_ReFire
    Goto Ready
  Spawn:
    NSAW A -1
    Stop
  }
}

ACTOR NegativeOnePistol : DoomWeapon
{
  Weapon.SlotNumber 2
  Weapon.SelectionOrder 1900
  Weapon.AmmoUse 1
  Weapon.AmmoGive 20
  Weapon.AmmoType "Clip"
  Obituary "$OB_MPPISTOL"
  +WEAPON.WIMPY_WEAPON
  Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED"
  Tag "$TAG_PISTOL"
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NPSG A 1 A_WeaponReady
    Loop
  Deselect:
    NPSG A 1 A_Lower
    Loop
  Select:
    NPSG A 1 A_Raise
    Loop
  Fire:
    NPSG B 0 A_FireBullets(0, 0, 1, 5, "NegativeOnePuff")
    NPSG B 0 A_PlaySound("negativeone/pistol", CHAN_WEAPON)
    NPSG B 6 A_GunFlash
    NPSG C 5
    Goto Ready
  Flash:
    NPSF A 6 Bright A_Light1
    Goto LightDone
  Spawn:
    PIST A -1
    Stop
  }
}
ACTOR NegativeOnePuff : BulletPuff
{
  States
  {
  Spawn:
    NGPF A 4 Bright
    NGPF B 4
    // Intentional fall-through
  Melee:
    NGPF CD 4
    Stop
  }
}

ACTOR NegativeOneShotgun : DoomWeapon
{
  Weapon.SlotNumber 3
  Weapon.SelectionOrder 1300
  Weapon.AmmoUse 1
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN"
  Obituary "$OB_MPSHOTGUN"
  Tag "$TAG_SHOTGUN"
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NSGG A 1 A_WeaponReady
    Loop
  Deselect:
    NSGG A 1 A_Lower
    Loop
  Select:
    NSGG A 1 A_Raise
    Loop
  Fire:
    NSGG A 3
    NSGG A 0 A_FireBullets (5.6, 0, 7, 5, "BulletPuff")
    NSGG A 0 A_PlaySound("negativeone/shotgf", CHAN_WEAPON)
    NSGG A 7 A_GunFlash
    NSGG BC 5
    NSGG D 4
    NSGG CB 5
    NSGG A 3
    NSGG A 7 A_ReFire
    Goto Ready
  Flash:
    NSGF A 4 Bright A_Light1
    NSGF B 3 Bright A_Light2
    Goto LightDone
  Spawn:
    NSHT A -1
    Stop
  }
}

ACTOR NegativeOneSuperShotgun : DoomWeapon
{
  Weapon.SlotNumber 3
  Weapon.SelectionOrder 400
  Weapon.AmmoUse 2
  Weapon.AmmoGive 8
  Weapon.AmmoType "Shell"
  Inventory.PickupMessage "$GOTSHOTGUN2"
  Obituary "$OB_MPSSHOTGUN"
  Tag "$TAG_SUPERSHOTGUN"
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NSG2 A 1 A_WeaponReady
    Loop
  Deselect:
    NSG2 A 1 A_Lower
    Loop
  Select:
    NSG2 A 1 A_Raise
    Loop
  Fire:
    NSG2 A 3
    NSG2 A 0 A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff")
    NSG2 A 0 A_PlaySound("negativeone/sshotgf", CHAN_WEAPON)
    NSG2 A 7 A_GunFlash
    NSG2 B 7
    NSG2 C 7 A_CheckReload
    NSG2 D 7 A_PlaySound("negativeone/sshotgo")
    NSG2 E 7
    NSG2 F 7 A_PlaySound("negativeone/sshotgl")
    NSG2 G 6
    NSG2 H 6 A_PlaySound("negativeone/sshotgc")
    NSG2 A 5 A_ReFire
    Goto Ready
  Flash:
    NSG2 I 4 Bright A_Light1
    NSG2 J 3 Bright A_Light2
    Goto LightDone
  Spawn:
    SGN2 A -1
    Stop
  }
}

ACTOR NegativeOneUzi : DoomWeapon
{
   Weapon.SlotNumber 4
   Weapon.SelectionOrder 700
   Weapon.AmmoUse 1
   Weapon.AmmoGive 20
   Weapon.AmmoType "Clip"
   Inventory.PickupMessage "You got an Uzi!" // "You got the chaingun"
   Obituary "%o was mowed down by %k's Uzi." // "%o was mowed down by %k's chaingun."
   Tag "Uzi"
   Inventory.PickupSound "negativeone/w_pkup"
   States
   {
   Ready:
     NCHG A 1 A_WeaponReady
     Loop
   Deselect:
     NCHG A 1 A_Lower
     Loop
   Select:
     NCHG A 1 A_Raise
     Loop
  Fire:
     NCHG A 0 A_PlaySound("negativeone/chngun", CHAN_WEAPON)
     NCHG A 0 A_GunFlash
     NCHG A 3 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
     NCHG B 0 A_PlaySound("negativeone/chngun", CHAN_WEAPON)
     NCHG B 0 A_GunFlash("Flash2")
     NCHG B 3 A_FireBullets(5.6, 0, 1, 5, "BulletPuff")
     NCHG B 0 A_ReFire
     Goto Ready
  Flash:
     NCHF A 3 Bright A_Light1
     Goto LightDone
  Flash2:
     NCHF B 3 Bright A_Light2
     Goto LightDone
   Spawn:
     NMGN A -1
     Stop
   }
}

ACTOR NegativeOneRocketLauncher : DoomWeapon
{
  Weapon.SlotNumber 5
  Weapon.SelectionOrder 2500
  Weapon.AmmoUse 1
  Weapon.AmmoGive 2
  Weapon.AmmoType "RocketAmmo"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "$GOTLAUNCHER"
  Tag "$TAG_ROCKETLAUNCHER"
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NMSG A 1 A_WeaponReady
    Loop
  Deselect:
    NMSG A 1 A_Lower
    Loop
  Select:
    NMSG A 1 A_Raise
    Loop
  Fire:
    NMSG B 8 A_GunFlash
    NMSG B 12 A_FireMissile
    NMSG B 0 A_ReFire
    Goto Ready
  Flash:
    NMSF A 3 Bright A_Light1
    NMSF B 4 Bright
    NMSF CD 4 Bright A_Light2
    Goto LightDone
  Spawn:
    NLAU A -1
    Stop
  }
}

ACTOR HellCharge : Cell
{
  Inventory.PickupMessage "Picked up a Hell charge." // "Picked up an energy cell."
  Inventory.Amount 20
  //Inventory.MaxAmount 300
  //Ammo.BackpackAmount 20
  //Ammo.BackpackMaxAmount 600
  //Inventory.Icon "NCLLA0"
  Inventory.PickupSound "negativeone/i_pkup"
  States
  {
  Spawn:
    NCLL A -1
    Stop
  }
}
ACTOR LargeHellCharge : Cell
{
  Inventory.PickupMessage "Picked up a large Hell charge." // "Picked up an energy cell pack."
  Inventory.Amount 100
  Inventory.PickupSound "negativeone/i_pkup"
  States
  {
  Spawn:
    NCLP A -1
    Stop
  }
}

ACTOR NegativeOneDarkSceptre : DoomWeapon
{
  Weapon.SlotNumber 6
  Weapon.SelectionOrder 100
  Weapon.AmmoUse 1
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  Inventory.PickupMessage "You got the Dark Sceptre, it glows with energy!"
  Tag "Dark Sceptre"
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NPLG A 1 A_WeaponReady
    Loop
  Deselect:
    NPLG A 1 A_Lower
    Loop
  Select:
    NPLG A 1 A_Raise
    Loop
  Fire:
    NPLG B 0 A_FireCustomMissile("NegativeOneDarkSceptreBall", 0, 1)
    NPLG B 5 A_GunFlash
    NPLG B 8 A_ReFire
    Goto Ready
  Flash:
    NPLF AB 2 Bright A_Light1
    Goto LightDone
  Spawn:
    NPLA A -1
    Stop
  }
}

ACTOR NegativeOneDarkSceptreBall : PlasmaBall
{
	SeeSound "negativeone/plasmaf"
	Obituary "%o was scorched by %k's Dark Sceptre." // "%o was melted by %k's plasma gun."
	Damage 6
	+SEEKERMISSILE
	States
	{
	Spawn:
		NPLS AABB 2 Bright A_Tracer
		loop
	Death:
		NPLE ABCDE 4 Bright
		stop
	}
}

ACTOR NegativeOneUnmakyr : DoomWeapon
{
  Height 20
  Weapon.SlotNumber 7
  Weapon.SelectionOrder 2800
  Weapon.AmmoUse 40
  Weapon.AmmoGive 40
  Weapon.AmmoType "Cell"
  +WEAPON.NOAUTOFIRE
  Inventory.PickupMessage "You got the Unmakyr 666,000! Oh yes..."
  Tag "Unmakyr 666,000"
  Inventory.PickupSound "negativeone/w_pkup"
  States
  {
  Ready:
    NBFG A 1 A_WeaponReady
    Loop
  Deselect:
    NBFG A 1 A_Lower
    Loop
  Select:
    NBFG A 1 A_Raise
    Loop
  Fire:
    NBFG A 20 A_PlaySound("negativeone/bfgf")
    NBFG B 10 A_GunFlash
    NBFG B 10 A_FireCustomMissile("NegativeOneUnmakyrBall", 0, 1)
    NBFG B 20 A_ReFire
    Goto Ready
  Flash:
    NBFG D 11 Bright A_Light1
    NBFG E 6 Bright A_Light2
    Goto LightDone
  Spawn:
    NBFU A -1
    Stop
  }
}

ACTOR NegativeOneUnmakyrBall : BFGBall
{
  DeathSound "negativeone/bfgx"
  Obituary "%o was vaporised by %k's Unmakyr."
  States
  {
  Spawn:
    NBFS AB 4 Bright
    Loop
  Death:
    NBF1 AB 8 Bright
    NBF1 C 8 Bright A_BFGSpray("UnmakyrExtra")
    NBF1 DEF 8 Bright
    Stop
  }
}

ACTOR UnmakyrExtra : BFGExtra
{
  Obituary "%o couldn't hide from %k's Unmakyr."
  States
  {
  Spawn:
    NBF2 ABCD 8 Bright
    Stop
  }
}