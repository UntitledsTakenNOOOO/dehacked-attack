/*
A FORWARD ON INVASION UAC'S INCLUSION & EXCLUSION

Invasion UAC is a ZDoom wad, released in 2008 to the best of my knowledge.

In a strange twist, despite being made in ZDoom, a port that uses DECORATE/ACS,
XSnake chose to implement his monsters with DEHACKED instead, weirdly enough.
As a result, Invasion UAC is a bit weird in being included, since it's not really
a 'traditional' use of DEHACKED, relative to the others.

As a result, I am not considering it 'mainline' accepted, however, due to the
strange circumstances surrounding it, I'm including two of its custom monsters
in the archive, while leaving their inclusion off by default.
*/

ACTOR DarkCyberdemon : Cyberdemon
{
  Health 5000 //Lowered from 10000
  Speed 22 //Lowered from 30
  PainChance 10
  Obituary "%o got exterminated by a dark cyberdemon."
  Tag "Dark Cyberdemon"
  DamageFactor "Heroic", 3.0
  +DONTHARMSPECIES
  States
  {
  Spawn:
    DCYB A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_INVASION_UAC, 11, 5), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("DEHCyberdemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    DCYB AB 10 A_Look
    Loop
  See:
    DCYB A 3 A_Hoof
    DCYB ABBCC 3 A_Chase
    DCYB D 3 A_Metal
    DCYB D 3 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    DCYB E 6 A_FaceTarget
    DCYB F 12 Bright A_CyberAttack
    DCYB E 0 A_BruisAttack
    DCYB E 0 A_SkelMissile
    DCYB E 0 A_FatAttack1
    DCYB E 0 A_FatAttack2
    DCYB E 0 A_FatAttack3
    DCYB E 12 A_FaceTarget
    Goto See
  LegendaryMissile:
    DCYB E 6 A_FaceTarget
    DCYB E 0 A_TroopAttack
    DCYB E 0 A_HeadAttack
    DCYB E 0 A_BSpiAttack
    DCYB E 0 A_BruisAttack
    DCYB E 0 A_SkelMissile
    DCYB E 0 A_FatAttack1
    DCYB E 0 A_FatAttack2
    DCYB E 0 A_FatAttack3
    DCYB F 12 Bright A_CyberAttack
    DCYB E 12 A_FaceTarget
    Goto See
  Pain:
    DCYB G 10 A_Pain
    Goto See
  Death:
    DCYB H 5
    DCYB I 5 A_Scream
    DCYB JKL 5
    DCYB M 5 A_NoBlocking
    DCYB NO 5
    DCYB P 15
    DCYB P -1 A_BossDeath
    Stop
  }
}

ACTOR WarlordOfHell : BaronOfHell
{
    Health 6000 //Lowered from 30000
    Radius 36
    Height 96
    Speed 18 //Lowered from 24
    PainChance 10
    Scale 1.5
    Mass 1500
    BloodColor "39 59 9F" //Added
    +FLOORCLIP
    +BOSS
	+DONTHARMCLASS
	//+DOHARMSPECIES //Not that it matters, since arch-vile attack.
	+BOSSDEATH
    Obituary "%o was extuingished by the warlord of hell."
    Tag "Warlord Of Hell"
	DamageFactor "Blessed", 3.0
	DamageFactor "Heroic", 3.0
    States
    {
	Spawn:
		BOS6 A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_INVASION_UAC, 18, 13), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHSpiderMastermind", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
        BOS6 AB 10 A_Look
        Loop
    See:
        BOS6 AABBCCDD 4 A_Chase
        Loop
    Melee:
    Missile:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_NoVileAttack") == 1, "AltMissile")
        BOS6 E 12 A_VileStart //Change: Added 8 frames of delay before attack on A_VileStart call
        BOS6 E 12 Bright A_VileTarget //Change: Upped delays 8 tics --> 12 tics
        BOS6 F 12 Bright A_FaceTarget
        BOS6 G 12 Bright A_VileAttack
        Goto See
	AltMissile:
        TNT1 A 0 Bright A_JumpIfHealthLower(1000, "AltMissile6")
        TNT1 A 0 Bright A_JumpIfHealthLower(2000, "AltMissile5")
        TNT1 A 0 Bright A_JumpIfHealthLower(3000, "AltMissile4")
        TNT1 A 0 Bright A_JumpIfHealthLower(4000, "AltMissile3")
        TNT1 A 0 Bright A_JumpIfHealthLower(5000, "AltMissile2")
		Goto AltMissile1
	AltMissile1:
		BOS6 A 0 A_ChangeFlag("MISSILEMORE", 1)
        BOS6 EF 4 Bright A_FaceTarget
        BOS6 G 4 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        BOS6 EF 4 Bright A_FaceTarget
        BOS6 G 4 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        BOS6 EF 4 Bright A_FaceTarget
        BOS6 G 4 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        Goto See
	AltMissile2:
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 8 Bright A_FatAttack1("WarlordOfHellBall")
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 8 Bright A_FatAttack2("WarlordOfHellBall")
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 8 Bright A_FatAttack3("WarlordOfHellBall")
        Goto See
	AltMissile3:
		BOS6 A 0 A_ChangeFlag("MISSILEMORE", 0)
		BOS6 A 0 A_ChangeFlag("MISSILEEVENMORE", 1)
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 8 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
		BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 0 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        BOS6 G 8 Bright A_FatAttack3("WarlordOfHellBall")
        Goto See
	AltMissile4:
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 8 Bright A_FatAttack3("WarlordOfHellBall")
		BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 0 Bright A_FatAttack1("WarlordOfHellBall")
        BOS6 G 8 Bright A_FatAttack2("WarlordOfHellBall")
        Goto See
	AltMissile5:
		BOS6 A 0 A_ChangeFlag("MISSILEMORE", 1)
		BOS6 A 0 A_ChangeFlag("MISSILEEVENMORE", 1)
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 1 Bright A_FatAttack1("WarlordOfHellBall")
        BOS6 G 1 Bright A_FatAttack2("WarlordOfHellBall")
        BOS6 G 4 Bright A_FatAttack3("WarlordOfHellBall")
        Goto See
	AltMissile6:
        BOS6 EF 8 Bright A_FaceTarget
        BOS6 G 1 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        BOS6 G 1 Bright A_FatAttack1("WarlordOfHellBall")
        BOS6 G 1 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        BOS6 G 1 Bright A_FatAttack2("WarlordOfHellBall")
        BOS6 G 1 Bright A_CustomComboAttack("WarlordOfHellBall", 32, 10 * random(1, 8), "baron/melee")
        BOS6 G 1 Bright A_FatAttack3("WarlordOfHellBall")
        Goto See
    Pain:
        BOS6 H 2
        BOS6 H 2 A_Pain
        Goto See
    Death:
        BOS6 I 12
        BOS6 J 12 A_Scream
        BOS6 K 12 A_Fall
        BOS6 LMN 12
        BOS6 O -1 A_BossDeath
        Stop
    Raise:
        Stop
    }
}

ACTOR WarlordOfHellBall : BaronBall
{
  Speed 25
  FastSpeed 30
  Damage 10
  Translation "112:127=192:207"
}