ACTOR ValiantImp : DoomImp
{
	Obituary "%o was scorched by a valiant imp."
	HitObituary "%o got scratched by a valiant imp."
	Tag "Valiant Imp"
	DamageFactor "Extinguishing", 3.0
	States
	{
   Spawn:
       TRO2 A 0
       TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 1, 3), "Reset")
       Goto Idle
   Reset:
       TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
       TNT1 A 0 A_SpawnItemEx("DEHDoomImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
       Stop
   Idle:
		TRO2 AB 10 A_Look
		Loop
	  See:
		TRO2 AABBCCDD 3 A_Chase
		Loop
	  Melee:
	  Missile:
		TRO2 EF 8 A_FaceTarget
		TRO2 G 6 A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		TRO2 VW 8 A_FaceTarget
		TRO2 X 6 A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		Goto See
	  Pain:
		TRO2 H 2
		TRO2 H 2 A_Pain
		Goto See
	  Death:
		TRO2 I 8
		TRO2 J 8 A_Scream
		TRO2 L 6 A_NoBlocking
		TRO2 M -1
		Stop
	  XDeath:
		TRO2 N 5
		TRO2 O 5 A_XScream
		TRO2 P 5
		TRO2 Q 5 A_NoBlocking
		TRO2 RST 5
		TRO2 U -1
		Stop
	  Raise:
		TROO MLKJI 8
		Goto See
	}
}

ACTOR ValiantImpBall
{
  Game Doom
  SpawnID 10
  Radius 6
  Height 8
  Speed 16
  FastSpeed 20
  Damage 3
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  SeeSound "imp/attack"
  DeathSound "imp/shotx"
  States
  {
  Spawn:
    VBL1 AB 4 Bright
    Loop
  Death:
    VBL1 CDE 6 Bright
    Stop
  }
}

ACTOR ValiantDemon : Demon
{
  Speed 12
  Obituary "%o was bit by a valiant demon."
  Tag "Valiant Demon"
  DamageFactor "Wrangler", 3.0
  States
  {
  Spawn:
    VSRG A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 1, 4), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHDemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    VSRG AB 10 A_Look
    Loop
  See:
    VSRG AABBCCDD 2  A_Chase
    Loop
  Melee:
    VSRG EF 3 A_FaceTarget
    VSRG G 18 A_SargAttack
    Goto See
  Pain:
    VSRG H 2 
    VSRG H 2 A_Pain
    Goto See
  Death:
    VSRG I 8
    VSRG J 8 A_Scream
    VSRG K 4
    VSRG L 4 A_NoBlocking
    VSRG M 4
    VSRG N -1
    Stop
  Raise:
    VSRG N 5
    VSRG MLKJI 5
    Goto See
  }
}

ACTOR ValiantSpectre : ValiantDemon
{
  +SHADOW
  RenderStyle OptFuzzy
  Alpha 0.5
  SeeSound "spectre/sight"
  AttackSound "spectre/melee"
  PainSound "spectre/pain"
  DeathSound "spectre/death"
  ActiveSound "spectre/active"
  HitObituary "%o was eaten by a valiant spectre."
  Tag "Valiant Spectre"
  DamageFactor "Wrangler", 3.0
  States
  {
  Spawn:
    VSRG A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 1, 4), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHSpectre", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    VSRG AB 10 A_Look
    Loop
  }
}

actor SuicideBomber
{
  obituary "%o hugged a suicide bomber."
  health 60
  Damage 96
  radius 20
  height 56
  mass 100
  speed 10
  painchance 5
  seesound "suicide/sit"
  attacksound "shotguy/death"
  painsound "shotguy/pain"
  deathsound "shotguy/death"
  MONSTER
  +FLOORCLIP
  DamageFactor "Extinguishing", 3.0
  DamageFactor "Wrangler", 3.0
  states
  {
  Spawn:
    BMBE A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 14, 13), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHDemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    BMBE AB 10 A_Look
    loop
  See:
    BMBE AAABBBCCCDDD 2 A_Chase
    BMBE A 0 A_PlaySound("suicide/scream")
    loop
  Pain:
    BMBE A 3
    BMBE A 3 A_Pain
    goto See
  Melee:
    Goto Death
  Death:
    BMBE M 0 bright A_Detonate
    BMBE M 5 bright A_PlaySound("weapons/rocklx")
    BMBE N 5 bright A_XScream
    BMBE O 5 A_NoBlocking
    BMBE PQRST 5
    BMBE U -1
    stop
  }
}


ACTOR ValiantBaronBall
{
  Game Doom
  SpawnID 154
  Radius 6
  Height 16
  Speed 30
  FastSpeed 40
  Damage 5
  Projectile
  +RANDOMIZE
  RenderStyle Add
  Alpha 1
  SeeSound "weapons/firbfi"
  DeathSound "baron/shotx"
  Decal "BaronScorch"
  States
  {
  Spawn:
    VBL7 AB 4 Bright
    Loop
  Death:
    VBL7 CDE 6 Bright
    Stop
  }
}

ACTOR ValiantPyroDemon : HellKnight
{
  Monster
  SeeSound "monster/ar2sit"
  PainSound "knight/pain"
  DeathSound "monster/ar2dth"
  ActiveSound "knight/active"
  Obituary "%o was burned to cinders by a Pyro Knight."
  HitObituary "%o got ripped to shreds by a Pyro Knight."
  Tag "Pyro Knight"
  DamageFactor "Blessed", 3.0
  DamageFactor "Extinguishing", 2.0
  States
  {
	Spawn:
		PYRO A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 9, 6), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHHellKnight", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		PYRO AB 10 Bright A_Look
		Loop
	See:
		PYRO AABBCCDD 3 Bright A_Chase
		Loop
	Missile:
		PYRO A 0 A_Jump(128,"AltMissile")
		PYRO E 4 Bright A_FaceTarget
		PYRO F 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO G 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO H 4 Bright A_CustomComboAttack("ValiantBaronBall", 32, 10*random(1,8), "baron/melee")
		PYRO I 4 Bright A_FaceTarget
		PYRO J 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO K 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO L 4 Bright A_CustomComboAttack("ValiantBaronBall", 32, 10*random(1,8), "baron/melee")
		Goto See
	AltMissile:
		PYRO M 8 Bright A_FaceTarget
		PYRO N 8 Bright A_FaceTarget
		PYRO OOPP 4 Bright A_CustomComboAttack("ValiantBaronBall", 32, 10*random(1, 8), "baron/melee")
		Goto See
	Melee:
		PYRO E 4 Bright A_FaceTarget
		PYRO F 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO G 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO H 4 Bright A_CustomComboAttack("ValiantBaronBall", 32, 10*random(1,8), "baron/melee")
		PYRO I 4 Bright A_FaceTarget
		PYRO J 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO K 4 Bright A_CustomComboAttack("ValiantImpBall", 32, 3*random(1,8), "imp/melee")
		PYRO L 4 Bright A_CustomComboAttack("ValiantBaronBall", 32, 10*random(1,8), "baron/melee")
		Goto See
	Pain:
		PYRO Q 2 Bright
		PYRO Q 2 Bright A_Pain
		Goto See
	Death:
		PYRO R 10 Bright A_Scream
		PYRO ST 10 Bright
		PYRO U 10 A_Fall
		PYRO VWX 6
		PYRO Y 3
		PYRO Z -1
		Stop
	Raise:
		PYRO Y 8
		PYRO XWVU 8
		PYRO TS 8 Bright
		Goto See
  }
}

ACTOR ValiantArachnotron : Arachnotron
{
	Tag "Valiant Arachnotron"
	DamageFactor "Scrapper", 3.0
	States
	{
	Spawn:
		VSPI A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 4, 8), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHArachnotron", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		VSPI AB 10 A_Look
		Loop
	See:
		VSPI A 20
		VSPI A 3 A_BabyMetal
		VSPI ABBCC 3 A_Chase
		VSPI D 3 A_BabyMetal
		VSPI DEEFF 3 A_Chase
		Goto See+1
	Missile:
		VSPI A 20 Bright A_FaceTarget
		VSPI G 4 Bright A_CustomMissile("ValiantArachnotronPlasma",32,0,0)
		VSPI H 4 Bright
		VSPI H 1 Bright A_SpidRefire
		Goto Missile+1
	Pain:
		VSPI I 3
		VSPI I 3 A_Pain
		Goto See+1
	Death:
		VSPI I 11 A_Scream
		VSPI A 0 A_Jump(170,"Death2") //Change: Upped to 67% (originally 50%)
		VSPI J 7 A_Fall
		VSPI KLM 7
		VSPI N 7
		VSPI P -1 A_BossDeath
		Stop
	Death2:
		VACB I 0 A_Fall
		VACB I 26 A_SpawnItem("ValiantArachnorb", 8)
		VACB J 11
		VACB K 7
		VACB L 7
		VACB M -1 A_BossDeath
		Stop
	Raise:
		VSPI P 5
		VSPI ONMLKJ 5
		Goto See+1
	}
}
ACTOR ValiantArachnotronPlasma : ArachnotronPlasma
{
  SeeSound "valiant/plasma"
  States
  {
  Spawn:
    VPLS AB 5 Bright
    Loop
  Death:
    VPBX ABCDE 5 Bright
    Stop
  }
}
ACTOR ValiantArachnorb
{
	Health 120
	Speed 12
	Radius 24
	Height 56
	Damage 0
	ReactionTime 8
	PainChance 150
	Mass 400
	Monster
	+FLOAT
	+NOGRAVITY
	SeeSound "aracnorb/sight"
	PainSound "baby/pain"
	DeathSound "aracnorb/death"
	ActiveSound "baby/active"
	Obituary "%o got blasted by an Arachnorb."
	Tag "Arachnorb"
	DamageFactor "Antiair", 3.0
	States
	{
	Spawn:
		VACB A 10 A_Look
		Goto Spawn
	See:
		VACB A 3 A_Chase
		Goto See
	Pain:
		VACB H 3
		VACB H 3 A_Pain
		Goto See
	Missile:
		VACB A 20 A_FaceTarget
		VACB BCB 9 Bright A_CustomMissile("ValiantArachnotronPlasma",32,0,0)
		VACB A 9 A_SpidRefire
		Goto Missile
	Raise:
		VACB FED 5
		Goto See
	Death:
		VACB D 5
		VACB E 5 A_Scream
		VACB F 5 A_Fall
		VACB G -1
		Stop
	}
}
ACTOR ValiantArachnorbSmaller : ValiantArachnorb
{
	Radius 16 //Changed to fit in lost soul spaces
	States
	{
	Spawn:
		VACB A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 4, 16), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHLostSoul", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		VACB A 10 A_Look
		Goto Spawn
	}
}

Actor ValiantSuperMancubus : Fatso
{
   Health 1000
   PainChance 80
   BloodColor "73 00 00"
   SeeSound "Daedabus/Sight"
   PainSound "fatso/pain"
   DeathSound "Daedabus/Death"
   ActiveSound "fatso/Active"
   Obituary "%o was torched by a super mancubus."
   Tag "Super Mancubus"
   DamageFactor "Scrapper", 3.0
   States
   {
   Spawn:
      DAED A 0
	  TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 19, 21), "Reset")
      Goto Idle
   Reset:
      TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
      TNT1 A 0 A_SpawnItemEx("DEHFatso", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
      Stop
   Idle:
      DAED AB 15 A_Look
      Loop
   See:
      DAED AABBCCDDEEFF 4 A_Chase
      Loop
   Pain:
      DAED J 3
      DAED J 3 A_Pain
      Goto See
   Missile:
      DAED G 20 A_FatRaise
      DAED H 3 Bright A_FatAttack1
      DAED H 3 Bright A_FatAttack2
      DAED H 4 Bright A_FatAttack3
      DAED IGGGG 5 A_FaceTarget
      DAED H 3 Bright A_FatAttack1
      DAED H 3 Bright A_FatAttack2
      DAED H 4 Bright A_FatAttack3
      DAED IG 5 A_FaceTarget
      Goto See
   Death:
      DAED K 6
      DAED L 6 A_Scream
      DAED M 6 A_NoBlocking
      DAED NOPQRS 6
      DAED T -1 A_BossDeath
      Stop
   Raise:
      DAED RQPONMLK 5
      Goto See
   }
}

ACTOR ValiantCybruiser : BaronOfHell
{ 
Health 500 
Radius 24 
Height 64 
Mass 1000 
Speed 8 
PainChance 20 
MONSTER
MeleeDamage 10
Obituary "%o was blown away by a Cybruiser."
HitObituary "%o was smashed by a Cybruiser."
Tag "Cybruiser"
+FLOORCLIP
+NORADIUSDMG
//Change: no +BOSS flag
SeeSound "monster/brusit" 
PainSound "baron/pain" 
DeathSound "monster/brudth"
MeleeSound "baron/melee"
DamageFactor "Blessed", 3.0
DamageFactor "Scrapper", 2.0
States 
   { 
   Spawn:
       CBRU A 0
	   TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 25, 20), "Reset")
       Goto Idle
   Reset:
       TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
       TNT1 A 0 A_SpawnItemEx("DEHHellKnight", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
       Stop
   Idle:
       CBRU AB 10 A_Look 
       Loop 
   See:
       CBRU A 1 A_Playsound("monster/bruwlk")
       CBRU A 2 A_Chase
       CBRU A 3 A_Chase
       CBRU BB 3 A_Chase
       CBRU C 1 A_Playsound("monster/bruwlk")
       CBRU C 2 A_Chase
       CBRU C 3 A_Chase 
       CBRU DD 3 A_Chase
       Loop 
   Missile:
       CBRU E 8 A_FaceTarget 
       CBRU F 12 Bright A_CyberAttack
       Goto See
   Melee:
       CBRU GH 8 A_FaceTarget
       CBRU I 8 A_BruisAttack
       Goto See
   Pain: 
       CBRU J 2 
       CBRU J 2 A_Pain 
       Goto See 
   Death: 
       CBRU K 6 Bright A_Scream
       CBRU LMN 6 Bright
       CBRU O 6 Bright A_NoBlocking
       CBRU QRS 6 Bright
       CBRU T -1
       Stop
   } 
}

ACTOR ValiantSuperVile : Archvile
{
	Health 3750 //Change: 7500 --> 3750 health for balance reasons
	Radius 20
	Height 56
	Mass 500
	Speed 20
	PainChance 10
	ReactionTime 4
	Monster
	MaxTargetRange 896
	+QUICKTORETALIATE
	+FLOORCLIP
	+NOTARGET
	+BOSS //Change: has +BOSS
	SeeSound "vile/sight"
	PainSound "vile/pain"
	DeathSound "vile/death"
	ActiveSound "vile/active"
	MeleeSound "vile/stop"
	Obituary "%o couldn't avoid the super archvile's fire."
	Tag "Super Archvile"
	DamageFactor "Extinguishing", 3.0
	DamageFactor "Heroic", 3.0
	States
	{
	Spawn:
		DIA2 A 0
	    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_VALIANT, 30, 21), "Reset")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossTargetCount") > 0, "Summoner")
		Goto SpawnArachnorbs
	SpawnArachnorbs:
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", 0, 72, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", 0, -72, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", 72, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", 72, 72, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", 72, -72, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", -72, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", -72, 72, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		TNT1 A 0 A_SpawnItemEx("ValiantArachnorb", -72, -72, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS, 0)
		Goto Idle
	Idle:
		DIA2 AB 10 A_Look
		Loop
	Summoner:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == FINAL_SUPERVILE, "Reset")
		TNT1 A 0 A_SpawnItemEx("ValiantSuperVileSummoner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Reset:
		TNT1 A 0 A_SpawnItemEx("DEHSpiderMastermind", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	See:
		DIAB AABBCCDDEEFF 2 A_VileChase
		Loop
	Missile:
		DIAB G 0 Bright A_VileStart
		DIAB G 10 Bright A_FaceTarget
		DIAB H 8 Bright A_VileTarget
		DIAB IJKLMN 8 Bright A_FaceTarget
		DIAB O 8 Bright A_VileAttack
		DIAB MN 8 Bright A_FaceTarget
		DIAB O 8 Bright A_VileAttack
		DIAB MN 8 Bright A_FaceTarget
		DIAB O 8 Bright A_VileAttack
		DIAB MN 8 Bright A_FaceTarget
		DIAB O 8 Bright A_VileAttack
		DIAB P 20 Bright
		Goto See
	Heal:
		DIA2 ABC 16 Bright
		Goto See
	Pain:
		DIAB Q 5
		DIAB Q 5 A_Pain
		Goto See
	Death:
		DIAB R 7
		DIAB S 7 A_Scream
		DIAB T 7
		DIAB U 7 A_Fall
		DIAB VWX 7
		DIAB Y 7
		DIAB Z -1 A_BossDeath
		Stop
	}
}

ACTOR ValiantSuperVileSummoner : ValiantSuperVile
{
	States
	{
	Spawn:
		DIA2 AB 10 Bright A_Look
		Goto Idle
	Idle:
		DIA2 AB 10 Bright A_Look
		Loop
	Missile:
		TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
		Goto "Super::Missile"
	LegendaryMissile:
		TNT1 A 0 A_Jump(32, "LegendaryMissile2")
		Goto "Super::Missile"
	LegendaryMissile2:
		DIA2 A 16 Bright
		DIAB AAAA 0 A_BrainSpit("ValiantArachnorbSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	Pain:
		DIAB Q 5 //Change: Spawns these guys on flinch since I don't have access
		DIAB Q 5 A_Pain //to map geometry needed to remake the fight perfectly
		DIA2 A 16 Bright
		DIAB AAAAAAAA 0 A_BrainSpit("ValiantArachnorbSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	}
}

ACTOR ValiantSuperVileFinal : ValiantSuperVile
{
	+NOTELESTOMP
	Health 7500
	var int user_count;
	var int user_spawned;
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecute("Dehacked_BossMultiBuff", 0, 0, 0, 0)
		goto Idle
	Idle:
		DIA2 AB 10 Bright A_Look
		Loop
	Pain:
		DIAB Q 5 //Change: Spawns these guys on flinch since I don't have access
		DIAB Q 5 A_Pain //to map geometry needed to remake the fight perfectly
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossEyeCount") > 10, "SuperSummonMonsters")
		Goto SummonMonsters
	SummonMonsters:
		DIA2 A 16 Bright
		DIA2 A 0 A_SetUserVar("user_spawned", user_spawned + 1)
		DIA2 A 0 A_JumpIf(user_spawned <= 4, "SpawnShot1")
		DIA2 A 0 A_JumpIf(user_spawned <= 6, "SpawnShot2")
		DIA2 A 0 A_JumpIf(user_spawned <= 8, "SpawnShot3") //from valve.wad
		DIA2 A 0 A_JumpIf(user_spawned <= 12, "SpawnShot4") //
		DIA2 A 0 A_JumpIf(user_spawned <= 15, "SpawnShot5") //
		DIA2 A 0 A_JumpIf(user_spawned <= 16, "SpawnShot6") //
		DIA2 AAAA 0 A_BrainSpit //Generic, will never get this far
		DIA2 BC 16 Bright
		Goto See
	SpawnShot1:
		DIA2 AAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantPyroDemonSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	SpawnShot2:
		DIA2 AAAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 A 0 A_BrainSpit("ValiantFatsoSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantCybruiserSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	SpawnShot3:
		DIA2 AAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantHellKnightSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	SpawnShot4:
		DIA2 AAAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 A 0 A_BrainSpit("ValiantFatsoSpawnShot")
		DIA2 A 0 A_BrainSpit("ValiantBaronOfHellSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	SpawnShot5:
		DIA2 AA 0 A_BrainSpit("ValiantBaronOfHellSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantArchvileSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	SpawnShot6:
		DIA2 A 0 A_BrainSpit("ValiantCyberdemonSpawnShot")
		DIA2 BC 16 Bright
		Goto See
	SuperSummonMonsters:
		DIA2 A 8 Bright
		DIA2 A 0 A_SetUserVar("user_spawned", user_spawned + 1)
		DIA2 A 0 A_JumpIf(user_spawned <= 4, "SuperSpawnShot1")
		DIA2 A 0 A_JumpIf(user_spawned <= 6, "SuperSpawnShot2")
		DIA2 A 0 A_JumpIf(user_spawned <= 8, "SuperSpawnShot3")
		DIA2 A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossEyeCount"))
		Goto BrainSpitLoop
	BrainSpitLoop:
		DIA2 A 0 A_BrainSpit
		DIA2 A 0 A_SetUserVar("user_count", user_count - 1)
		DIA2 A 0 A_JumpIf(user_count <= 0, "BrainSpitEnd")
		Loop
	BrainSpitEnd:
		DIA2 BC 16 Bright
		Goto See
	SuperSpawnShot1:
		DIA2 AAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantPyroDemonSpawnShot")
		DIA2 BCA 8 Bright
		DIA2 AAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantHellKnightSpawnShot")
		DIA2 BC 8 Bright
		Goto See
	SuperSpawnShot2:
		DIA2 AAAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 A 0 A_BrainSpit("ValiantFatsoSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantCybruiserSpawnShot")
		DIA2 BCA 8 Bright
		DIA2 AAAAA 0 A_BrainSpit("ValiantRevenantSpawnShot")
		DIA2 A 0 A_BrainSpit("ValiantFatsoSpawnShot")
		DIA2 A 0 A_BrainSpit("ValiantBaronOfHellSpawnShot")
		DIA2 BC 8 Bright
		Goto See
	SuperSpawnShot3:
		DIA2 AA 0 A_BrainSpit("ValiantBaronOfHellSpawnShot")
		DIA2 AA 0 A_BrainSpit("ValiantArchvileSpawnShot")
		DIA2 BCA 8 Bright
		DIA2 A 0 A_BrainSpit("ValiantCyberdemonSpawnShot")
		DIA2 BC 8 Bright
		Goto See
	Death:
		DIAB R 7 A_PlaySound("brain/death", 0, 1.0, 0, ATTN_NONE)
		DIAB S 7 A_Scream
		DIAB T 7
		DIAB U 7 A_Fall
		DIAB VWX 7
		DIAB Y 7
		DIAB Z 64
		DIAB Z -1 A_BrainDie
		Stop
	}
}
ACTOR ValiantArachnorbSpawnShot : SpawnShot
{
  Speed 20
  DropItem "ValiantArachnorb", 256, 1
  States
  {
  Spawn:
    TNT1 A 0 BRIGHT A_SpawnItemEx("DEHSpawnShotFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 1 BRIGHT A_SpawnFly("TeleportFog")
    Loop
  }
}
ACTOR ValiantRevenantSpawnShot : ValiantArachnorbSpawnShot
{
  DropItem "DEHRevenant", 256, 1
}
ACTOR ValiantPyroDemonSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "ValiantPyroDemon", 256, 1
}
ACTOR ValiantFatsoSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "DEHFatso", 256, 1
}
ACTOR ValiantCybruiserSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "ValiantCybruiser", 256, 1
}
ACTOR ValiantHellKnightSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "HellKnight", 256, 1
}
ACTOR ValiantBaronOfHellSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "BaronOfHell", 256, 1
}
ACTOR ValiantArchvileSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "Archvile", 256, 1
}
ACTOR ValiantCyberdemonSpawnShot : ValiantRevenantSpawnShot
{
  DropItem "Cyberdemon", 256, 1
}