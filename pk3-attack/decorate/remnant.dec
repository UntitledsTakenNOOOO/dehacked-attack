ACTOR RemnantCardinal : DEHCyberdemon
{
  Health 4500 //Change: Lowered health from 5000 to 4500
  Speed 16
  PainChance 20
  +FLOAT
  +NOGRAVITY
  +DONTHARMSPECIES
  -NORADIUSDMG //Deliberate
  Obituary "%o was splattered by a Remnant Cardinal."
  SeeSound "cardinal/sight"
  DeathSound "cardinal/death"
  Tag "Remnant Cardinal"
  DamageFactor "Heroic", 3.0
  DamageFactor "Antiair", 2.0
  States
  {
  Spawn:
    RCRD A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_REMNANT, 1, 21), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHCyberdemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    RCRD AB 3 A_Look
	Loop
  See:
    RCRD AB 3 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
	RCRD C 6 A_FaceTarget
    RCRD D 6 Bright A_CyberAttack
    RCRD C 6 A_FaceTarget
    RCRD D 6 Bright A_CyberAttack
    RCRD C 6 A_FaceTarget
    RCRD D 6 Bright A_CyberAttack
    RCRD C 6 A_FaceTarget
    RCRD D 6 Bright A_CyberAttack
    Goto See
  LegendaryMissile:
    RCRD C 4 A_FaceTarget
    RCRD D 4 Bright A_CyberAttack
    RCRD C 4 A_FaceTarget
    RCRD D 4 Bright A_CyberAttack
    RCRD C 4 A_FaceTarget
    RCRD D 4 Bright A_CyberAttack
    RCRD C 4 A_FaceTarget
    RCRD D 4 Bright A_CyberAttack
    Goto See
  Pain:
    RCRD E 10 A_Pain
    Goto See
  Death:
    RCRD F 10
    RCRD G 10 A_Scream
    RCRD HIJ 10
    RCRD K 10 A_NoBlocking
    RCRD L 10 A_BossDeath
    Stop
  }
}

ACTOR RemnantMaster
{
  Health 6000
  Radius 60
  Height 60
  Mass 1000
  Speed 18
  PainChance 10
  Monster
  MinMissileChance 160
  +BOSS
  +MISSILEMORE
  +FLOORCLIP
  +NORADIUSDMG
  +DONTMORPH
  +BOSSDEATH
  +FLOAT
  +NOGRAVITY
  SeeSound "master/sight"
  AttackSound ""
  PainSound "master/pain"
  DeathSound "master/death"
  ActiveSound ""
  Obituary "%o stood in awe of the Remnant Master."
  Tag "Remnant Master"
  DamageFactor "Heroic", 3.0
  DamageFactor "Antiair", 2.0
  States
  {
  Spawn:
    UNMB A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_REMNANT, 1, 17), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("GateMaster", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    UNMB AB 15 A_Look
    Loop
  See:
    UNMB AABB 3 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    UNMB D 6 A_FaceTarget
    UNMB E 10 Bright A_SkelMissile
    UNMB E 5 Bright A_SkelMissile
    UNMB E 20 Bright A_FatAttack3
    UNMB D 20 A_FaceTarget
    UNMB E 10 Bright A_SkelMissile
    UNMB E 5 Bright A_SkelMissile
    UNMB E 20 Bright A_FatAttack3
    Goto See
  SuperMissile:
    UNMB D 6 A_FaceTarget
    UNMB E 10 Bright A_SkelMissile
    UNMB E 5 Bright A_SkelMissile
    UNMB E 5 Bright A_FatAttack1
    UNMB E 5 Bright A_FatAttack2
    UNMB E 10 Bright A_FatAttack3
    UNMB D 20 A_FaceTarget
    UNMB E 10 Bright A_SkelMissile
    UNMB E 5 Bright A_SkelMissile
    UNMB E 5 Bright A_FatAttack1
    UNMB E 5 Bright A_FatAttack2
    UNMB E 10 Bright A_FatAttack3
    Goto See
  LegendaryMissile:
    TNT1 A 0 A_Jump(64, "PainMissile")
    TNT1 A 0 A_Jump(85, "SuperMissile")
    Goto Missile+1
  Pain:
    UNMB F 12 A_Pain
    Goto PainMissile
  PainMissile:
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_NoVileAttack") == 1, "SuperMissile")
	UNMB O 0 A_VileStart
	UNMB O 10 Bright A_FaceTarget
	UNMB P 10 Bright A_VileTarget
	UNMB Q 10 Bright A_FaceTarget 
	UNMB R 6 Bright A_VileAttack
    Goto See
  Death:
    UNMB G 10
    UNMB H 10 A_Scream
    UNMB I 10 A_Fall
    UNMB JKLM 10
    UNMB N 30 A_SetFloorClip
    UNMB N -1 A_BossDeath
    Stop
  }
}