// D64D2 monsters and ZDoom-only additions
// Mechadon 2017-2018

/*
The Motherdemon is final boss that appears in MAP30.  This replaces the
64MTer Mastermind and ends the level shortly after it dies.

Its main attack consists of a flurry of Revenant and Mancubus fireballs followed
up with an Archvile flame attack.  When it goes into its pain state, it will
recoil and then follow up by spawning a Lost Soul.

This entry adds custom obit messages and removes the attack sound.
*/
ACTOR MotherdemonFinal
{
	Health 9000
	Radius 40
	Height 105
	Mass 10000
	Speed 25
	PainChance 4
	Monster
	MinMissileChance 160
	+BOSS
	+MISSILEMORE
	+FLOORCLIP
	+NORADIUSDMG
	+DONTMORPH
	+BOSSDEATH
	+FLOAT
	+NOGRAVITY
	+NOTELEFRAG  //Adding this to help mitigate easy MAP30 telefrag kills
	SeeSound "mother/sight"
	AttackSound ""
	PainSound "mother/pain"
	DeathSound "mother/death"
	ActiveSound "mother/active"
	Obituary "%o witnessed the Motherdemon's power."
	DamageFactor "Heroic", 3.0
	DamageFactor "Antiair", 2.0
	Tag "Motherdemon"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecute("Dehacked_BossMultiBuff", 0, 0, 0, 0)
		goto Idle
	Idle:
		64MT AB 10 A_Look
		Loop
	See:
		64MT AAABBBCCCDDD 2 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
		64MT E 10 A_FaceTarget
		64MT FF 2 Bright A_SkelMissile
		64MT F 0 Bright A_FatAttack3
		64MT G 2 Bright A_SkelMissile
		64MT G 0 Bright A_FatAttack1
		64MT G 1 Bright A_FatAttack2
		64MT G 1 Bright A_FatAttack3
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_NoVileAttack") == 1, "AltMissile")
		64MT H 0 Bright A_VileStart
		64MT H 10 Bright A_FaceTarget
		64MT H 8 Bright A_VileTarget
		64MT HH 8 Bright A_FaceTarget
		64MT IIII 8 Bright A_FaceTarget
		64MT J 8 Bright A_VileAttack
		64MT J 20 Bright
		Goto See
	Pain:
		64MT K 40 A_Pain
		Goto Summon
	AltMissile:
		64MT H 0 Bright A_VileStart
		64MT H 10 Bright A_FaceTarget
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT H 0 Bright A_FatAttack1("DoomImpBall")
		64MT H 1 Bright A_FatAttack2("DoomImpBall")
		64MT HH 8 Bright A_FaceTarget
		64MT IIII 8 Bright A_FaceTarget
		64MT J 0 A_CustomMissile("CacodemonBall", 114, -48, 0) // TOP LEFT
		64MT J 0 A_CustomMissile("CacodemonBall",  76, -64, 0) // BOTTOM LEFT
		64MT J 0 A_CustomMissile("CacodemonBall", 114,  48, 0) // TOP RIGHT
		64MT J 3 A_CustomMissile("CacodemonBall",  76,  64, 0) // BOTTOM RIGHT
		64MT J 0 A_CustomMissile("CacodemonBall", 114, -48, 0) // TOP LEFT
		64MT J 0 A_CustomMissile("CacodemonBall",  76, -64, 0) // BOTTOM LEFT
		64MT J 0 A_CustomMissile("CacodemonBall", 114,  48, 0) // TOP RIGHT
		64MT J 3 A_CustomMissile("CacodemonBall",  76,  64, 0) // BOTTOM RIGHT
		64MT J 0 A_CustomMissile("RevenantTracer", 114, -48,  90) // TOP LEFT
		64MT J 0 A_CustomMissile("RevenantTracer",  76, -64,  90) // BOTTOM LEFT
		64MT J 0 A_CustomMissile("RevenantTracer", 114,  48, -90) // TOP RIGHT
		64MT J 3 A_CustomMissile("RevenantTracer",  76,  64, -90) // BOTTOM RIGHT
		64MT J 0 A_CustomMissile("RevenantTracer", 114, -48,  90) // TOP LEFT
		64MT J 0 A_CustomMissile("RevenantTracer",  76, -64,  90) // BOTTOM LEFT
		64MT J 0 A_CustomMissile("RevenantTracer", 114,  48, -90) // TOP RIGHT
		64MT J 3 A_CustomMissile("RevenantTracer",  76,  64, -90) // BOTTOM RIGHT
		64MT J 16 Bright
		Goto See
	Summon:
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossEyeCount") > 10, "SuperSummon")
		64MT H 10 Bright A_FaceTarget
		64MT H 8 Bright A_PainAttack("DEHLostSoul")
		64MT H 8 Bright A_DualPainAttack("WillOWisp")
		64MT H 8 Bright A_PainAttack("DEHLostSoul")
		64MT I 8 Bright A_DualPainAttack("DamnedSoul")
		64MT I 8 Bright A_PainAttack("DEHLostSoul")
		64MT I 8 Bright A_DualPainAttack("Doppleganger")
		64MT I 8 Bright A_PainAttack("DEHLostSoul")
		64MT J 8 Bright A_DualPainAttack("AlphaSoul")
		64MT J 20 Bright A_PainAttack("PoisonSoul")
		Goto See
	SuperSummon:
		64MT H 10 Bright A_FaceTarget
		64MT H 7 Bright A_PainAttack("DEHCacodemon")
		64MT H 7 Bright A_PainAttack("Toxicacodemon")
		64MT H 7 Bright A_PainAttack("SpectralCaco")
		64MT H 3 Bright A_PainAttack("Cacodemoness")
		64MT I 4 Bright
		64MT I 7 Bright A_PainAttack("Cacolich")
		64MT I 7 Bright A_PainAttack("Slomnibus")
		64MT I 7 Bright A_PainAttack("NightmareCacodemon")
		64MT I 7 Bright A_PainAttack("AstralCaco")
		64MT J 7 Bright A_PainAttack("DEHPainElemental")
		64MT J 7 Bright A_PainAttack("FlameCaco")
		64MT J 7 Bright A_PainAttack("SpiritElemental")
		64MT J 7 Bright A_PainAttack("NightwatchCacodemon")
		Goto See
	LegendaryMissile:
		64MT E 10 A_FaceTarget
		64MT FF 2 Bright A_SkelMissile
		64MT F 0 Bright A_FatAttack3
		64MT G 2 Bright A_SkelMissile
		64MT G 0 Bright A_FatAttack1
		64MT G 1 Bright A_FatAttack2
		64MT G 1 Bright A_FatAttack3
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_NoVileAttack") == 1, "AltMissile")
		TNT1 A 0 Bright A_Jump(96, "Summon")
		64MT H 0 Bright A_VileStart
		64MT H 10 Bright A_FaceTarget
		64MT H 8 Bright A_VileTarget
		64MT HH 8 Bright A_FaceTarget
		64MT IIII 8 Bright A_FaceTarget
		64MT J 8 Bright A_VileAttack
		64MT J 20 Bright
		Goto See
	Death:
		64MT L 0 A_PlaySound("brain/death", 0, 1.0, 0, ATTN_NONE)
		64MT L 20 A_Scream
		64MT M 10 A_Fall
		64MT NO 8
		64MT PQRS 6
		64MT T 48
		64MT T -1 A_BrainDie
		Stop
	}
}

ACTOR Motherdemon : MotherdemonFinal
{
	Health 5300 //Change: 5300 Health, like in original Doom 64
	Speed 20 //Change: Speed 25 --> 20 for balance reasons
	PainChance 20 //Change: PainChance 4 --> 20 (Like in original D64)
	-NOTELEFRAG
	States
	{
	Spawn:
		64MT A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == FINAL_MOTHERDEMON, "Reset")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DOOM64, 30, 21), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("Hierophant2", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		64MT AB 10 A_Look
		Loop
	See:
		64MT AAABBBCCCDDD 2 A_Chase
		Loop
	Missile:
		//CHANGE: Attack pattern reworked for cyberdemon slot
		//Attack is more remniscient of the original Doom 64 Mother Demon
		64MT H 5 Bright A_FaceTarget
		64MT I 5 Bright A_FaceTarget
		64MT J 1 Bright A_FatAttack1
		64MT J 1 Bright A_FatAttack2
		64MT J 1 Bright A_FatAttack1
		64MT J 1 Bright A_FatAttack2
		64MT J 1 Bright A_FatAttack1
		64MT J 1 Bright A_FatAttack2
		64MT J 1 Bright A_FatAttack1
		64MT J 1 Bright A_FatAttack2
		64MT J 1 Bright A_FatAttack1
		64MT J 1 Bright A_FatAttack2
		64MT EF 10 A_FaceTarget
		64MT G 0 A_CustomMissile("RevenantTracer", 114, -48,  90) // TOP LEFT
		64MT G 0 A_CustomMissile("RevenantTracer",  76, -64,  90) // BOTTOM LEFT
		64MT G 0 A_CustomMissile("RevenantTracer", 114,  48, -90) // TOP RIGHT
		64MT G 10 A_CustomMissile("RevenantTracer",  76,  64, -90) // BOTTOM RIGHT
		Goto See
	Pain:
		64MT K 40 A_Pain
		Goto See
	Death:
		64MT L 20 A_Scream
		64MT M 10 A_Fall
		64MT NO 8
		64MT PQRS 6
		64MT T 48
		64MT T -1 A_BossDeath
		Stop
	}
}

/*
The Nightmare Imp is a 'spectre' version of the Imp (a fuzzy Imp).  It uses
Cacodemon projectiles for its attack.  Its the same as the regular Imp in every 
other way.

The ZDoom version uses new sprites for its fireball, and it also gives it a 
melee attack sound.  Custom obit messages are also added here.
*/
ACTOR NightmareImp : DoomImp
{
	DamageFactor "Extinguishing", 3.0
	Tag "Nightmare Imp"
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
	HitObituary "%o was slashed by a Nightmare Imp."
	Obituary "%o was burned by a Nightmare Imp."
	States
	{
	Spawn:
		TROO A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DOOM64, 3, 3), "Reset")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_SubtractiveReset"), "SubtractiveReset")
		Goto "Super::Spawn"
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHDoomImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	SubtractiveReset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("NightmareImpSubtract", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_HeadAttack
		Goto See
	}
}
ACTOR NightmareImpSubtract : NightmareImp
{
	RenderStyle Subtract
	Alpha 0.95
	States
	{
	Spawn:
		TROO A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DOOM64, 3, 3), "Reset")
		Goto Idle
	Idle:
		TROO AB 10 A_Look
		Loop
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHDoomImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Melee:
	Missile:
		TROO EF 8 A_FaceTarget
		TROO G 6 A_CustomComboAttack("NightmareImpBall", 32, 10 * random(1, 6), "imp/melee")
		Goto See
	}
}
ACTOR NightmareImpBall : CacodemonBall
{
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		BALN AB 4 Bright
		Loop
	Death:
		BALN CDE 6 Bright
		Stop
	}
}

/*
The Nightmare Cacodemon is similar to the Nightmare Imp in that its a fuzzy
version of the original Cacodemon, but with a few minor changes.  Its a bit
faster and is less susceptible to pain, and it also has a triple-barrage
projectile attack.
*/
ACTOR NightmareCacodemon : Cacodemon
{
	Speed 12
	PainChance 96
	+SHADOW
	RenderStyle OptFuzzy
	Alpha 0.5
	Obituary "%o was smitten by a Nightmare Cacodemon."
	HitObituary "%o got too close to a Nightmare Cacodemon."
	DamageFactor "Antiair", 3.0
	Tag "Nightmare Cacodemon"
	States
	{
	Spawn:
		HEAD A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DOOM64, 32, 17), "Reset")
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_SubtractiveReset"), "SubtractiveReset")
		Goto "Super::Spawn"
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("SpectralCaco", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	SubtractiveReset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("NightmareCacodemonSubtract", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Melee:
	Missile:
		HEAD BC 5 A_FaceTarget
		HEAD DDD 3 Bright A_HeadAttack
		Goto See
	}
}

ACTOR NightmareCacodemonSubtract : NightmareCacodemon
{
	RenderStyle Subtract
	Alpha 0.95
	States
	{
	Spawn:
		HEAD A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DOOM64, 32, 17), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("SpectralCaco", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		HEAD A 10 A_Look
		Loop
	Melee:
	Missile:
		HEAD BC 5 A_FaceTarget
		HEAD DDD 3 Bright A_CustomComboAttack("NightmareImpBall", 32, 10 * random(1, 6), "imp/melee")
		Goto See
	}
}