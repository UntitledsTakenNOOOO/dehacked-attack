ACTOR AngryMastermind : SpiderMastermind
{
  Health 6000 //Change: 12000 --> 6000 health for balance reasons
  Obituary "%o stood in awe of the angry spiderdemon."
  Tag "Angry Spiderdemon"
  PainSound "brain/pain"
  DamageFactor "Heroic", 3.0
  States
  {
  Spawn:
    RSPD A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == FINAL_ANGRYMIND, "Reset")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_GOING_DOWN, 29, 20), "Reset")
    Goto Idle
  Idle:
    RSPD AB 10 Bright A_Look
    Loop
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("SpiderMastermind", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  See:
    RSPD A 3 A_Metal
    RSPD ABB 3 A_Chase
    RSPD C 3 A_Metal
    RSPD CDD 3 A_Chase
    RSPD C 3 A_Metal
    RSPD CBB 3 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    RSPD A 10 Bright A_FaceTarget
    RSPD EG 4 Bright A_SPosAttackUseAtkSound
    RSPD E 1 Bright A_SpidRefire
    Goto Missile+2
  LegendaryMissile:
    TNT1 A 0 A_Jump(128, "PainMissile")
    Goto Missile+1
  PainMissile:
    RSPD A 10 Bright A_FaceTarget
    RSPD E 4 Bright A_FatAttack1
    RSPD G 4 Bright A_FatAttack2
    RSPD E 4 Bright A_SkelMissile
    RSPD G 4 Bright A_SkelMissile
    Goto See
  Pain:
    RSPD H 3
    RSPD H 3 A_Pain
    Goto PainMissile
  Death:
    RSPD I 20 A_Scream
    RSPD N 10 A_NoBlocking
    RSPD OPQRSTU 10
    RSPD V 30
    RSPD V -1 A_BossDeath
    Stop
  }
}

ACTOR AngryMastermindFinalPhase1 : SpiderMastermind
{
  +NOTELESTOMP
  var int user_count;
  DamageFactor "Heroic", 3.0
  States
  {
  Death:
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop1
  Death1:
	SPID J 20 A_Scream
	SPID K 10 A_NoBlocking
	SPID L 10
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop2
  Death2:
	SPID MNOP 10
	SPID A 0 A_SetUserVar("user_count", ACS_NamedExecuteWithResult("Dehacked_BossTargetCount"))
	Goto SpawnerLoop3
  Death3:
	SPID QR 10
	SPID SSS 10 A_BrainSpit("GDVileSpawnShot")
	SPID S 0 A_SpawnItemEx("TeleportFog", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
	SPID S -1 A_SpawnItemEx("AngryMastermindFinalPhase2", 0, 0, 0, 0, 0, 0, 0, SXF_TELEFRAG|SXF_TRANSFERPOINTERS, 0)
	Stop
  SpawnerLoop1:
	TNT1 A 0 A_BrainSpit("GDSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "Death1")
	Loop
  SpawnerLoop2:
	TNT1 A 0 A_BrainSpit("GDSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "Death2")
	Loop
  SpawnerLoop3:
	TNT1 A 0 A_BrainSpit("GDCacoSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "Death3")
	Loop
  SpawnerLoop4:
	TNT1 AAA 0 A_BrainSpit("GDVileSpawnShot")
	TNT1 A 0 A_SetUserVar("user_count", user_count - 1)
	TNT1 A 0 A_JumpIf(user_count <= 0, "DeathFinal")
	Loop
  }
}
ACTOR GDSpawnShot : SpawnShot
{
  Speed 30
  DropItem "DoomImp", 256, 64
  DropItem "Zombieman", 256, 32
  DropItem "Shotgunguy", 256, 32
  States
  {
  Spawn:
    TNT1 A 0 BRIGHT A_SpawnItemEx("DEHSpawnShotFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 1 BRIGHT A_SpawnFly("TeleportFog")
    Loop
  }
}
ACTOR GDCacoSpawnShot : SpawnShot
{
  Speed 30
  DropItem "Cacodemon", 256, 1
  States
  {
  Spawn:
    TNT1 A 0 BRIGHT A_SpawnItemEx("DEHSpawnShotFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 1 BRIGHT A_SpawnFly("TeleportFog")
    Loop
  }
}
ACTOR GDVileSpawnShot : SpawnShot
{
  Speed 30
  DropItem "Archvile", 256, 1
  States
  {
  Spawn:
    TNT1 A 0 BRIGHT A_SpawnItemEx("DEHSpawnShotFog",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
    TNT1 A 1 BRIGHT A_SpawnFly("TeleportFog")
    Loop
  }
}
ACTOR AngryMastermindFinalPhase2 : AngryMastermind
{
  Health 12000
  +MISSILEEVENMORE
  +NOTELESTOMP
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 ACS_NamedExecute("Dehacked_BossMultiBuff", 0, 0, 0, 0)
    goto Idle
  Idle:
    RSPD AB 10 Bright A_Look
    Loop
  Pain:
    RSPD H 3
    RSPD H 3 A_BrainPain
    Goto PainMissile
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossEyeCount") > 10, "SuperMissile")
    RSPD A 10 Bright A_FaceTarget
    RSPD EG 4 Bright A_SPosAttackUseAtkSound
    RSPD E 1 Bright A_SpidRefire
    Goto Missile+3
  SuperMissile:
    RSPD A 10 Bright A_FaceTarget
    RSPD E 2 Bright A_SPosAttackUseAtkSound
    RSPD E 2 Bright A_FatAttack1
    RSPD G 2 Bright A_SPosAttackUseAtkSound
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_SPosAttackUseAtkSound
    RSPD E 2 Bright A_FatAttack2
    RSPD G 2 Bright A_SPosAttackUseAtkSound
    RSPD G 2 Bright A_SkelMissile
    RSPD E 1 Bright A_SpidRefire
    Goto SuperMissile+1
  LegendaryMissile:
    TNT1 A 0 A_Jump(128, "PainMissile")
    Goto Missile+1
  PainMissile:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossEyeCount") > 10, "SuperPainMissile")
    RSPD A 10 Bright A_FaceTarget
    RSPD E 4 Bright A_FatAttack1
    RSPD G 4 Bright A_SkelMissile
    RSPD E 4 Bright A_FatAttack2
    RSPD G 4 Bright A_SkelMissile
    RSPD E 4 Bright A_FatAttack3
    Goto See
  SuperPainMissile:
    RSPD A 10 Bright A_FaceTarget
    RSPD E 2 Bright A_FatAttack1
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack3
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack2
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack3
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack1
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack3
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack2
    RSPD G 2 Bright A_SkelMissile
    RSPD E 2 Bright A_FatAttack3
    RSPD G 2 Bright A_SkelMissile
    Goto See
  Death:
    RSPD I 20 A_PlaySound("brain/death", 0, 1.0, 0, ATTN_NONE)
    RSPD N 10 A_NoBlocking
    RSPD OPQRSTU 10
    RSPD V 30
    RSPD V -1 A_BrainDie
    Stop
  }
}