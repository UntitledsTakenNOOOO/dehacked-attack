//Untitled: This is taken directly from NOVA2.wad, aside from a few changes.
//Any commented-out code is a change, alongside labeled comments.

//This is the DECORATE file for the new final boss, the Hierophant. This enables the Hierophant to be classed 
//as a monster as opposed to the decoration it replaces, enabling it to perform monster functions in 
//ZDoom-derived ports.

//-Obsidian

//Hierophant First Form
Actor Hierophant1 //Replaces Meat5
{
  Health 750
  Radius 20 //24
  Height 56 //60
  Mass 1000
  Speed 14
  PainChance 24
  Monster
  DamageType Normal
  +Float
  +NoGravity
  //-COUNTKILL
  //-ISMONSTER
  SeeSound "monster/hiesit"
  PainSound "vile/pain"
  DeathSound "misc/teleport"
  ActiveSound "vile/active"
  Tag "Hierophant"
  DamageFactor "Antiair", 3.0
  DamageFactor "Extinguishing", 2.0
  States
  {
  Spawn:
    HIER A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_NOVA2, 30, 16), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("Archvile", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    HIER A 10 A_Look
    Loop
  See:
    HIER AABB 3 A_Chase
    Goto See
  Missile:
    HIER CD 8 A_FaceTarget
    HIER EE 0 A_SkelMissile
    HIER E 8 A_SkelMissile
    Goto See
  Pain:
    HIER I 4 A_Pain
    Goto See
  Death: 
    TFOG A 6 BRIGHT A_Scream 
    TFOG B 6 BRIGHT A_KeenDie 
    TFOG ABCDEFGHIJ 6 BRIGHT
    Stop
    } 
}

//Hierophant Second Form
Actor Hierophant2 : Hierophant1 //Replaces BloodyTwitch
{
  Health 5400 //8000
  Radius 24
  Height 60
  Mass 10000
  Speed 15
  +Boss
  SeeSound "monster/hiesit"
  PainSound "monster/hiepai"
  DeathSound "monster/hiedth"
  ActiveSound "monster/hieact"
  Tag "Hierophant"
  DamageFactor "Extinguishing", 1.0
  DamageFactor "Antiair", 2.0
  DamageFactor "Heroic", 3.0
  States
  {
  Spawn:
    HIER A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == FINAL_HIEROPHANT, "Reset")
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_NOVA2, 30, 15), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("Afrit", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    HIER A 10 A_Look
    Loop
  See:
    HIER AB 6 A_Chase
    Goto See
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
	HIER F 8 A_FaceTarget
    HIER G 4 A_FaceTarget
    HIER H 4 BRIGHT A_BruisAttack
	HIER G 1 A_CposRefire
    Goto Missile+2
  Pain:
    HIER I 2
    HIER I 2 A_Pain 
    HIER CD 5 A_FaceTarget
    HIER E 0 A_FatAttack1
	HIER E 0 A_FatAttack2
    HIER E 5 A_FatAttack3
    Goto See
  LegendaryMissile:
    HIER F 8 A_FaceTarget
    HIER G 0 A_Jump(16, "LegendaryMissile2")
    HIER G 4 A_FaceTarget
    HIER H 4 BRIGHT A_BruisAttack
	HIER G 1 A_CPosRefire
    Goto LegendaryMissile+1
  LegendaryMissile2:
    HIER CD 5 A_FaceTarget
    HIER E 0 A_FatAttack1
	HIER E 0 A_FatAttack2
    HIER E 5 A_FatAttack3
    Goto See
  Death:
    HIER J 6
    HIER K 6 A_Scream //A_BrainScream
    HIER L 6 A_Fall
    HIER MNO 6
	HIER P 5
	//HIER Q 120
	HIER Q -1 //A_BrainDie
    Stop
  }
}

//All of this is new.
Actor FinalBossHierophant : Hierophant2
{
  Health 8000
  +NoTelefrag
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 ACS_NamedExecute("Dehacked_BossMultiBuff", 0, 0, 0, 0)
    goto Idle
  Idle:
    HIER A 10 A_Look
    Loop
  Death:
    HIER J 6
    HIER K 6 A_Scream
    HIER L 6 A_Fall
    HIER MNO 6
	HIER P 5
	HIER Q 120 A_BrainScream
	HIER Q -1 A_BrainDie
    Stop
  }
}