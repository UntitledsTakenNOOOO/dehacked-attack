Actor SpiritImp : DEHDoomImp
{
  Health 125
  Speed 16
  PainChance 170
  Mass 200
  BloodColor "DarkGreen"
  Obituary "%o was killed by a Spirit Imp."
  Tag "Spirit Imp"
  States
  {
  Spawn:
    SHTR A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHEX, 10, 13), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("DoomImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    SHTR A 3 A_Look
    Loop
  See:
    SHTR BBDDCCEE 3 A_Chase
    Loop
  Melee:
  Missile:
    SHTR F 8 A_FaceTarget
    SHTR G 8 Bright A_FaceTarget
    SHTR G 10 Bright A_CustomMissile("SpiritImpBall",32,0,0)
    Goto See
  Pain:
    SHTR H 6
    SHTR H 6 A_Pain
    Goto See
  Death:
    SHTR I 8
    SHTR J 8 A_Scream
    SHTR K 6
    SHTR L 6 A_NoBlocking
    SHTR M -1
    Stop
  Raise:
    SHTR ML 8
    SHTR KJI 6
    Goto See
  }
}
ACTOR SpiritImpBall
{
  Radius 13
  Height 8
  Speed 25
  Damage 5
  Projectile
  +RANDOMIZE
  RenderStyle Add 
  Alpha 0.75
  SeeSound "baron/attack"
  DeathSound "baron/shotx"
  States
  {
  Spawn:
    ACID AB 5 Bright A_SpawnItemEx("SpiritImpBallFX",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
    Loop
  Death:
    ACID CD 5 Bright
	ACID E 5 A_PainAttack("LostSpirit")
    Stop
  }
}
ACTOR SpiritImpBallFX
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  RenderStyle Add
  Alpha 0.75
  States
  {
  Spawn:
    ACID CDE 5 Bright
    Stop  
  }
}

ACTOR LostSpirit
{
  Health 5
  Radius 14
  Height 32
  Mass 50
  Speed 7
  PainChance 256
  Monster
  +FLOAT
  +NOGRAVITY
  +DONTFALL
  +NOICEDEATH
  -SOLID
  AttackSound "skull/melee"
  PainSound "skull/pain"
  DeathSound "skull/death"
  ActiveSound "skull/active"
  RenderStyle SoulTrans
  Obituary "%o was spooked by a lost spirit." // "%o was spooked by a lost soul."
  BloodColor "DarkGreen"
  Tag "Spirit"
  DamageFactor "Antiair", 3.0 //Like this actually matters at 5 HP
  States
  {
  Spawn:
    SHSP JIHGFE 3 Bright
    Goto Idle
  Idle:
    SHSP A 3 Bright A_Look
    Loop
  See:
    SHSP AB 5 Bright A_Chase
    Loop
  Missile:
    SHSP A 6 Bright A_FaceTarget
    SHSP B 6 Bright A_BruisAttack
	Goto See
  Pain:
    SHSP AB 5 Bright
    Goto See
  Death:
    SHSP E 3 Bright A_NoBlocking
    SHSP FGH 3 Bright
    SHSP IJ 3
    Stop
  }
}

ACTOR SpiritElemental : DEHPainElemental
{
  Obituary "%o was killed by a Spirit Elemental."
  BloodColor "DarkGreen"
  Tag "Spirit Elemental"
  States
  {
  Spawn:
    SHPA A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHEX, 10, 13), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("DEHPainElemental", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    SHPA A 10 A_Look
    Loop
  See:
    SHPA AABBCC 3 A_Chase
    Loop
  Missile:
    SHPA D 5 A_FaceTarget
    SHPA E 5 Bright A_CustomMissile("SpiritImpBall",32,0,0)
    SHPA F 5 Bright A_FaceTarget
    SHPA F 0 Bright A_CustomMissile("SpiritImpBall",32,0,0)
    Goto See
  Pain:
    SHPA G 6
    SHPA G 6 A_Pain
    Goto See
  Death:
    SHPA H 8 Bright
    SHPA I 8 Bright A_Scream
    SHPA JK 8 Bright
    SHPA L 8 Bright A_PainDie("LostSpirit")
    SHPA M 8 Bright
    Stop
  Raise:
    SHPA MLKJIH 8
    Goto See
  }
}