Actor GreaterMancubus : DEHFatso
{
	Health 650
	Speed 9
	Painchance 80
	Mass 1000
	Monster
	+FLOAT
	+NOGRAVITY
	+BOSSDEATH //Redundant, but added for consistency because I hate MAP07
	SeeSound "fatso/sight"
	PainSound "fatso/pain"
	DeathSound "fatso/death"
	ActiveSound "fatso/active"
	Tag "Greater Mancubus"
	DamageFactor "Antiair", 2.0
	DamageFactor "Scrapper", 3.0
	States
	{
	Spawn:
		GRFT A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_MAYANMISHAP, 1, 13, 1), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHFatso", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		GRFT A 10 A_Look
		Loop
	See:
		GRFT A 3 A_Chase
		Loop
	Missile:
		GRFT B 0 A_Jump(128, "Missile2")
		GRFT D 20 A_FatRaise
		GRFT C 10 Bright A_FatAttack3
		GRFT B 8 A_Facetarget
		GRFT C 0 Bright A_FatAttack1
		GRFT C 10 Bright A_FatAttack2
		GRFT D 7 A_FaceTarget
		Goto See
	Missile2:
		GRFT B 20 A_FatRaise
		GRFT C 10 Bright A_FatAttack3
		GRFT D 8 A_FaceTarget
		GRFT C 0 Bright A_FatAttack2
		GRFT C 10 Bright A_FatAttack1
		GRFT B 7 A_FaceTarget
		Goto See
	Pain:
		GRFT E 4
		GRFT E 4 A_Pain
		Goto See
	Death:
		GRFT F 6
		GRFT G 6 A_Scream
		GRFT H 6 A_Fall
		GRFT IJK 6
		GRFT L -1 A_KillMaster
		Stop
	Raise:
		GRFT LKJIHGF 5
		Goto See
	}
}

Actor CyberBaron : DEHBaronofHell
{
	Health 900
	Speed 10
	Radius 24
	Height 64
	Reactiontime 8
	Painchance 50
	Mass 1000
	Monster
	SeeSound "cyberbaron/sight"
	PainSound "cyberbaron/pain"
	DeathSound "cyberbaron/death"
	ActiveSound "cyberbaron/active"
	Tag "Cyber Baron"
	DamageFactor "Blessed", 3.0
	DamageFactor "Scrapper", 2.0
	States
	{
	Spawn:
		CBOS A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_MAYANMISHAP, 1, 8), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DEHBaronOfHell", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		CBOS A 10 A_Look
		Loop
	See:
		CBOS AABBCCDD 3 A_Chase
		Loop
	Pain:
		CBOS H 2
		CBOS H 2 A_Pain
		Goto See
	Melee:
		CBOS PQ 4 A_FaceTarget
		CBOS R 6 A_BruisAttack
		CBOS PQ 4 A_FaceTarget
		CBOS R 6 A_BruisAttack
		Goto See
	Missile:
		CBOS R 0 A_Jump(96, "Missile2")
		Goto Melee
	Missile2:
		CBOS E 1 A_PlaySound("cyberbaron/sight")
		CBOS E 11 A_FaceTarget
		CBOS E 12 A_FaceTarget
		CBOS F 6 Bright A_CyberAttack
		Goto See
	Death:
		CBOS I 8
		CBOS J 8 A_Scream
		CBOS K 8
		CBOS L 8 A_Fall
		CBOS MN 8
		CBOS O -1 A_BossDeath
		Stop
	Raise:
		CBOS ONMLKJI 8
		Goto See
	}
}