ACTOR SubvertedGuard : Chaingunguy
{
  DamageFactor "Exorcist", 3.0
  Obituary "%o was perforated by a subverted guard."
  Tag "Subverted Guard"
  States
  {
  Spawn:
    CGPS A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 1, 1), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHChaingunguy", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    CGPS AB 10 A_Look
    Loop
  See:
    CGPS AABBCCDD 3 A_Chase
    Loop
  Missile:
    CGPS E 10 A_FaceTarget
    CGPS FE 4 Bright A_CPosAttack
    CGPS F 1 A_CPosRefire
    Goto Missile+1
  Pain:
    CGPS G 3
    CGPS G 3 A_Pain
    Goto See
  Death:
    CGPS H 5
    CGPS I 5 A_Scream
    CGPS J 5 A_NoBlocking
    CGPS KLM 5
    CGPS N -1
    Stop
  XDeath:
    CGPS O 5 
    CGPS P 5 A_XScream
    CGPS Q 5 A_NoBlocking
    CGPS RS 5
    CGPS T -1
    Stop
  Raise:
    CGPS N 5
    CGPS MLKJIH 5
    Goto See
  }
}

ACTOR StrainFastImp : DoomImp
{
  DamageFactor "Extinguishing", 3.0
  SeeSound "fastimp/sight"
  HitObituary "%o was shredded by a class-2 fast imp."
  Obituary "%o let a class-2 fast imp get him."
  Tag "Class-2 Fast Imp"
  States
  {
  Spawn:
    TROO A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 3, 7), "Reset")
    Goto "Super::Spawn"
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("ValiantImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  See:
    TROO AAAABBBBCCCCDDDD 1 A_Chase
    Loop
  Melee:
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    TROO EF 8 A_FaceTarget
    TROO G 6 A_TroopAttack
    TROO G 0 A_SpidRefire
    Loop
  LegendaryMissile:
    TROO EF 5 A_FaceTarget
    TROO G 5 A_TroopAttack
    TROO G 0 A_SpidRefire
    Loop
  }
}

ACTOR Doppleganger : LostSoul //Where do they get these names from?
{
  DamageFactor "Antiair", 3.0
  Obituary "%o was spooked by a doppleganger."
  Damage 2
  Tag "Doppleganger"
  PainChance 200
  Height 48 //Don't ask me why it's shorter - it's like that in STRAIN.
  States
  {
  Spawn:
    SKL2 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 4, 7), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHLostSoul", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    SKL2 AB 10 Bright A_Look
    Loop
  See:
    SKL2 AB 6 Bright A_Chase
    Loop
  Missile:
    SKL2 C 10 Bright A_FaceTarget
    SKL2 D 10 Bright A_TroopAttack
    SKL2 CDC 10 Bright A_FaceTarget
    SKL2 D 4 Bright A_SkullAttack
    SKL2 CD 4 Bright
    Goto Missile+6
  Pain:
    SKL2 E 3 Bright
    SKL2 E 3 Bright A_Pain
    Goto See
  Death:
    SKL2 F 6 Bright
    SKL2 G 6 Bright A_Scream
    SKL2 H 6 Bright
    SKL2 I 6 Bright A_NoBlocking
    SKL2 J 6
    SKL2 K 6
    Stop
  }
}

ACTOR Holobot : Cacodemon
{
  DamageFactor "Antiair", 3.0
  Health 200
  Speed 10
  PainSound ""
  ActiveSound "holobot/active"
  SeeSound "holobot/sight"
  DeathSound "holobot/death"
  MeleeSound ""
  Obituary "%o was unable to hide from a holobot."
  Tag "Holobot"
  States
  {
  Spawn:
    FBOT A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 6, 5), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHCacodemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    FBOT A 3 A_Look
    Loop
  See:
    FBOT ABCDEF 3 A_Chase
    Loop
  Melee:
    FBOT A 20 A_FaceTarget
    FBOT G 6 A_CustomMeleeAttack(random(1, 10) * 6, "holobot/melee", "holobot/melee")
    Goto See
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    FBOT AA 5 A_FaceTarget
    FBOT A 5 Bright A_CustomMissile("HolobotRevenantTracer", 32)
    Goto See
  LegendaryMissile:
    FBOT AA 5 A_FaceTarget
    FBOT A 5 Bright A_CustomMissile("RevenantTracer", 32)
    Goto See
  Pain:
    FBOT A 3
    FBOT A 3 A_Pain
    FBOT A 6
    Goto See
  Death:
    FBOT H 10 Bright
    FBOT I 10 Bright A_Scream
    FBOT J 10 Bright A_NoBlocking
    FBOT K 10 Bright
    FBOT L 10 Bright
    FBOT M 10 Bright
    Stop
  Raise:
    Stop
  }
}
ACTOR HolobotRevenantTracer : RevenantTracer
{
  Speed 4
}
ACTOR InvisibleHolobot : HoloBot
{
  DamageFactor "Antiair", 3.0
  +SHADOW
  RenderStyle "OptFuzzy"
  Alpha 0.5
  Obituary "%o was lost sight of an invisible holobot."
  Tag "Invisible Holobot"
  States
  {
  Spawn:
    FBOT A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 11, 5), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHPainElemental", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  }
}

ACTOR StrainDemon : Demon
{
  DamageFactor "Wrangler", 3.0
  Obituary "%o was bit by a skinless demon."
  Tag "Skinless Demon"
  Health 217 // I've always found it weird how it has exactly 217 health.
  States // Maybe there's something about the number I'm missing, but's strange.
  {
  Spawn:
    SAR2 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 2, 7), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHDemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    SAR2 AB 10 A_Look
    Loop
  See:
    SAR2 AABBCCDD 2 Fast A_Chase
    Loop
  Melee:
    SAR2 EF 8 Fast A_FaceTarget
    SAR2 G 8 Fast A_SargAttack
    Goto See
  Pain:
    SAR2 H 2 Fast
    SAR2 H 2 Fast A_Pain
    Goto See
  Death:
    SAR2 I 8
    SAR2 J 8 A_Scream
    SAR2 K 4
    SAR2 L 4 A_NoBlocking
    SAR2 M 4
    SAR2 N -1
    Stop
  Raise:
    SAR2 N 5
    SAR2 MLKJI 5
    Goto See
  }
}

ACTOR MinisterOfHate
{
  DamageFactor "Scrapper", 3.0
  Health 300
  Radius 20
  Height 55
  Mass 300
  Speed 9
  PainChance 40
  Monster
  +FLOORCLIP
  +NORADIUSDMG
  SeeSound "cyberjr/sight"
  PainSound "cyber/pain"
  DeathSound "cyber/death"
  ActiveSound "cyber/active"
  Tag "Minister of Hate"
  Obituary "%o was splattered by a Minister of Hate."
  States
  {
  Spawn:
    CYBJ A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 13, 12), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("HellCleric", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    CYBJ AB 10 A_Look
    Loop
  See:
    CYBJ A 3 A_PlaySound("cyberjr/hoof", CHAN_BODY, 1.0, 0, ATTN_NORM)
    CYBJ ABBCC 3 A_Chase
    CYBJ D 3 A_Metal
    CYBJ D 3 A_Chase
    Loop
  Missile:
    CYBJ E 12 A_FaceTarget
    CYBJ F 12 Bright A_CyberAttack
    Goto See
  Pain:
    CYBJ G 10 A_Pain
    Goto See
  Death:
    CYBJ H 5
    CYBJ I 5 A_Scream
    CYBJ JKL 5
    CYBJ M 5 A_NoBlocking
    CYBJ NO 5
    CYBJ P -1
    Stop
  }
}

ACTOR StrainDemonLord : BaronOfHell
{
  DamageFactor "Blessed", 3.0
  SeeSound "demonlord/sight"
  DeathSound "demonlord/death"
  Health 1400
  Obituary "%o was flattened by a Demon Lord."
  HitObituary "%o was ripped apart by a Demon Lord."
  Tag "Demon Lord"
  States //He doesn't actually have a melee attack, interestingly enough.
  {
  Spawn:
    BOSL A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_STRAIN, 15, 21), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("Belphegor", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    BOSL AB 10 A_Look
    Loop
  See:
    BOSL AABBCCDD 3 A_Chase
    Loop
  Melee: //Change: fixed the animation so he attacks at the right speed.
  Missile:
    BOSL EF 8 A_FaceTarget
    BOSL G 1 A_FatAttack1("DemonLordBall")
    BOSL G 1 A_FatAttack2("DemonLordBall")
    BOSL G 6 A_FatAttack3("DemonLordBall")
    Goto See
  Pain:
    BOSL H 2
    BOSL H 2 A_Pain
    Goto See
  Death:
    BOSL I 8
    BOSL J 8 A_Scream
    BOSL K 8
    BOSL L 8 A_NoBlocking
    BOSL MN 8
    BOSL O -1 A_BossDeath
    Stop
  Raise:
    BOSL O 8
    BOSL NMLKJI 8
    Goto See
  }
}
ACTOR DemonLordBall : BaronBall
{
  Speed 15
  Damage 5
}