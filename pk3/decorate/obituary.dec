/*
A FORWARD ON OBITUARY'S INCLUSION & EXCLUSION

Obiturary, not well-known, is a 1995 doom wad that is likely the first implementation of custom monsters in DEHACKED.

The three monsters featured in it were originally implemented in the confines of DEHACKED.
However, they were later re-implemented in the ZDoom Monster Resource Wad (and later Realm667).

Due to a combination of obscurity and the fact that the most famous versions of these monsters are DECORATE
(and not DEHACKED) I am not considering them part of the mainline DEHACKED Attack.

That being said, in respect for the legacy of this wad, I am including them in the mod,
however, they will be turned off by default, though the option to turn them on exists.
*/

ACTOR SuperImp : DoomImp
{
	DamageFactor "Extinguishing", 3.0
	PainChance 50
	Obituary "%o was shocked by the imp's superior firepower."
	Tag "Harder Imp"
	States
	{
	Spawn:
		TROO A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_OBITUARY, 1, 7), "Reset")
		Goto "Super::Spawn"
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("DoomImp", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Melee:
	Missile:
		IMP3 AB 8 A_FaceTarget
		TROO G 8 A_BruisAttack
		Goto See
	}
}

ACTOR RocketGuy
{
   DamageFactor "Exorcist", 3.0
   Health 50 
   Radius 20 
   Height 56 
   Mass 100 
   Speed 8 
   PainChance 200 
   Monster 
   +FLOORCLIP 
   SeeSound "grunt/sight" 
   PainSound "grunt/pain" 
   Deathsound "grunt/death" 
   ActiveSound "grunt/active"
   DropItem "RocketAmmo"
   Obituary "%o was splattered by a bazooka guy's rocket."
   Tag "Bazooka Guy"
   States 
   { 
  Spawn:
    MPOS A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_OBITUARY, 1, 20), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("Shotgunguy", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
   Idle:
      MPOS AB 10 A_Look 
      Loop 
   See: 
      MPOS AABBCCDD 3 A_Chase 
      Loop 
   Missile: 
      MPOS EE 10 A_FaceTarget 
      MPOS F 5 
      MPOS G 13 Bright A_CyberAttack 
      Goto See 
   Pain: 
      MPOS H 2 
      MPOS H 2 A_Pain 
      Goto See 
   Death: 
      MPOS I 5 
      MPOS J 5 A_Scream 
      MPOS K 5 
      MPOS L 5 A_Fall 
      MPOS M -1 
      Stop 
   XDeath: 
      MPOS N 5 
      MPOS O 5 A_XScream 
      MPOS P 5 
      MPOS Q 5 A_Fall 
      MPOS RSTU 5 
      MPOS V -1    
      Stop 
   Raise: 
      MPOS MJKJI 5 
      Goto See 
   } 
}

ACTOR StealthFighter
{
	DamageFactor "Exorcist", 3.0
	Health 60
	Radius 22
	Height 56
	PainChance 150
	ReactionTime 6
	Speed 8
	Monster 
	+FLOORCLIP
	+NOBLOOD
	SeeSound "shadow/sight"
	DeathSound "shadow/death"
	Attacksound "shotguy/attack"
	Obituary "%o never got a clear picture of his killer."
	Tag "High-tech Guy"
	States
	{
	Spawn:
		GMAN A 0
		TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_OBITUARY, 1, 21), "Reset")
		Goto Idle
	Reset:
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_SpawnItemEx("Shotgunguy", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
		Stop
	Idle:
		GMAN AB 10 A_Look
		Loop
	See:
		GMAN AABBCCDD 1 A_Chase
		Loop
	Missile:
		GMAN E 7 A_FaceTarget
		GMAN F 7 A_FaceTarget
		GMAN G 1 Bright A_CPosAttack
		GMAN F 3 A_FaceTarget
		GMAN G 3 Bright A_CPosAttack
		GMAN F 1 A_CPosRefire
		Loop
	Pain:
		GMAN H 3
		GMAN H 3 A_Pain
		Goto See
	Death:
		GMAN I 5
		GMAN J 5 A_Scream
		GMAN K 5 A_Fall
		GMAN L 5
		GMAN M -1
		Stop
	XDeath:
		GMAN N 5 
		GMAN O 5 A_XScream
		GMAN QRSTU 5
		GMAN V -1
		Stop
	Raise:
		GMAN M 5
		GMAN LKJI 5
		Goto See 
	}
}