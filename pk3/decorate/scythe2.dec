actor EvilMarine
{
  obituary "%o was killed by an evil marine."
  health 250
  radius 20
  height 56
  mass 100
  speed 16
  painchance 160
  SeeSound "shotguy/sight"
  PainSound "shotguy/pain"
  DeathSound "shotguy/death"
  ActiveSound "shotguy/active"
  dropitem "cell"
  translation 2
  Tag "Evil Marine"
  Monster
  DamageFactor "Exorcist", 2.0
  DamageFactor "Scrapper", 2.0
  DamageFactor "Extinguishing", 2.0
  States
  {
  Spawn:
    PLAY A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHE, 16), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("Archvile", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    PLAY AB 10 A_look
    Loop
  See:
    PLAY AAABBBCCCDDD 1 A_Chase
    Loop
  Missile:
    PLAY E 1 A_facetarget
	PLAY F 2 Bright A_BspiAttack
	PLAY E 1 A_facetarget
	PLAY F 2 Bright A_BspiAttack
	PLAY E 1 A_facetarget
	PLAY F 2 Bright A_BspiAttack
	PLAY E 1 A_facetarget
    Goto See
  Pain:
    PLAY G 4 
    PLAY G 4 A_Pain
    Goto See
  Death:
    PLAY H 5
	PLAY I 5 A_scream
	PLAY J 5 A_Noblocking
	PLAY K 5
	PLAY L -1
	Stop
  XDeath:
    PLAY M 10
    PLAY N 10 A_Scream
    PLAY O 10 A_NoBlocking
    PLAY PQRSTUV 5
    PLAY W -1
    Stop
  Raise: //Change: No resurrection for you, because archvile slot.
	Stop
  }
}

//Resurgence's variant of the Evil Marine
actor ResurgenceEvilMarine : EvilMarine
{
  Speed 3
  PainChance 30
  States
  {
  Spawn:
    PLAY A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_RESURGENCE, 21), "Reset")
    Goto Idle
  Reset:
	TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("HellCleric", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
  Idle:
    PLAY AB 10 A_look
    Loop
  Raise:
	PLAY L 5
	PLAY KJIH 51
	Goto See
  }
}

actor Afrit : BaronOfHell
{
  Health 2500
  PainChance 30
  +NoGravity
  +Float
  +DoHarmSpecies
  +Boss //CHANGE: Added to be more in line with the other minibosses.
  Obituary "%o was scorched by an Afrit."
  Tag "Afrit"
  DamageFactor "Heroic", 2.0
  DamageFactor "Blessed", 2.0
  DamageFactor "Antiair", 2.0
  DamageFactor "Extinguishing", 2.0
  States
  {
  Spawn:
    FRIT A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DEUS_VULT, 21), "Spawn2")
    Goto CheckCyberCount
  Spawn2:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_RESURGENCE, 21), "Spawn3")
    Goto CheckCyberCount
  Spawn3:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHE, 26), "Spawn4")
    Goto CheckCyberCount
  Spawn4:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SURVIVE_IN_HELL, 15), "Reset")
    Goto CheckCyberCount
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHCyberdemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  CheckCyberCount:
	TNT1 A 1
	TNT1 A 1 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_CyberCount"), "Reset")
	Goto Idle
  Idle:
    FRIT AABB 10 Bright A_Look
    Loop
  See:
    FRIT AABBCCDD 3 Bright A_Chase
    Loop
  Melee:
  Missile:
	FRIT S 8 Bright A_FaceTarget
	FRIT T 8 Bright A_FaceTarget
    FRIT U 1 Bright A_FatAttack1
	FRIT U 1 Bright A_FatAttack2
	FRIT U 1 Bright A_FatAttack3
	FRIT U 1 Bright A_FaceTarget
	FRIT U 1 Bright A_SkelMissile
	FRIT U 1 Bright A_SkelMissile
	FRIT U 6 Bright A_SkelMissile
    Goto See
  Pain:
    FRIT H 4 Bright
    FRIT H 4 Bright A_Pain
    Goto See
  Death:
    FRIT I 6 Bright A_Scream
    FRIT J 5 Bright A_NoBlocking
    FRIT KLMNOPQ 4 Bright
	FRIT R 4 Bright A_BossDeath
    Stop
  }
}

actor AfritSummon : Afrit
{
  -Boss
  DamageFactor "Heroic", 1.0
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_DEUS_VULT, 21), "Spawn2")
    Goto Idle
  Spawn2:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_RESURGENCE, 21), "Spawn3")
    Goto Idle
  Spawn3:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SCYTHE, 26), "Spawn4")
    Goto Idle
  Spawn4:
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SURVIVE_IN_HELL, 15), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("BaronOfHell", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  }
}