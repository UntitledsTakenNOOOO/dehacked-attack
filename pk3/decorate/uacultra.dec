actor Haymaker : Cyberdemon
{
  Obituary "%o was floored by a Haymaker."
  HitObituary "%o was hammered by a Haymaker."
  Health 2500
  Speed 14
  PainChance 35
  Monster
  +FLOORCLIP
  +DONTMORPH
  SeeSound "haymaker/sight"
  DeathSound "haymaker/death"
  Tag "Haymaker"
  DamageFactor "Heroic", 3.0
  states
  {
  Spawn:
    HMKR A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_UAC_ULTRA, 7), "Reset")
    Goto CheckCyberCount
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("DEHCyberdemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  CheckCyberCount:
	TNT1 A 1
	TNT1 A 1 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_CyberCount"), "Reset")
	Goto Idle
  Idle:
    HMKR A 10 A_Look
    loop
  See:
    HMKR ABCD 2 A_Chase
    loop
  Missile:
    TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
    HMKR E 6 A_FaceTarget
    HMKR F 0 A_FatAttack1
    HMKR F 6 A_FatAttack2
    HMKR G 6
    goto See
  LegendaryMissile:
    HMKR E 6 A_FaceTarget
    HMKR F 0 A_FatAttack1
    HMKR F 6 A_FatAttack2
    HMKR G 6 A_FatAttack3
    goto See
  Pain:
    HMKR H 10 A_Pain
    goto See
  Death:
    HMKR I 6 A_Scream
    HMKR JKL 6
    HMKR M 6 A_NoBlocking
    HMKR NOPQRSTUV 6
    HMKR V -1
    stop
  }
}

actor HaymakerSummon : Haymaker
{
  -NORADIUSDMG
  -BOSS
  states
  {
  Spawn:
    HMKR A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_UAC_ULTRA, 1), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("AfritSummon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  Idle:
    HMKR A 10 A_Look
    loop
  }
}