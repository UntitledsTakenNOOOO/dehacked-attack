ACTOR Belphegor : BaronOfHell
{
   Health 1500
   Obituary "%o was incinerated by a Belphegor."
   HitObituary "the last thing %o saw was the Belphegor's abs."
   Tag "Belphegor"
   DamageFactor "Blessed", 3.0
   States
   {
  Spawn:
    BOS3 A 0
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SURVIVE_IN_HELL, 2), "Reset")
    Goto Idle
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
	TNT1 A 0 A_SpawnItemEx("BaronOfHell", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
	Stop
   Idle:
      BOS3 AB 10 A_Look
      Loop
   See:
      BOS3 AABBCCDD 3 A_Chase
      Loop
   Melee:
   Missile:
      TNT1 A 0 A_JumpIfInventory("LDLegendaryMonsterTransformed", 1, "LegendaryMissile")
	  BOS3 EF 6 A_FaceTarget
      BOS3 G 6 A_BruisAttack
      BOS3 F 6 A_FaceTarget
      BOS3 G 6 A_BruisAttack
      Goto See
   LegendaryMissile:
      BOS3 EF 8 A_FaceTarget
      BOS3 G 4 A_BruisAttack
      BOS3 EF 4 A_FaceTarget
      BOS3 G 4 A_BruisAttack
      BOS3 EF 4 A_FaceTarget
      BOS3 G 4 A_BruisAttack
      Goto See
   Pain:
      BOS3 H 2
      BOS3 H 2 A_Pain
      Goto See
   Death:
      BOS3 I 8
      BOS3 J 8 A_Scream
      BOS3 K 8
      BOS3 L 8 A_NoBlocking
      BOS3 MN 8
      BOS3 O -1
      Stop
   Raise:
      BOS3 ONMLKJI 8
      Goto See
   }
}

ACTOR PoisonSoul
{
Health 500
Radius 16
Height 56
Mass 50
Speed 8
RENDERSTYLE SoulTrans
PAINCHANCE 255
MONSTER
+NOGRAVITY
+FLOAT
+dontfall
AttackSound "skull/melee"
PainSound "skull/pain"
DeathSound "skull/death"
ActiveSound "skull/active"
Tag "Poison Soul"
Obituary "%o was poisoned by a Poison Soul."
HitObituary "%o was chomped on by a Poison Soul."
DamageFactor "Antiair", 3.0
States
   {
   Spawn:
       SKLL A 0
       TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SURVIVE_IN_HELL, 11), "Reset")
       Goto Idle
   Reset:
       TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
       TNT1 A 0 A_SpawnItemEx("ShadowElemental", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
       Stop
   Idle:
       SKLL A 10 A_Look
       Loop
   See:
       SKLL A 3 A_Chase
       Loop
   Missile:
       SKLL B 10 Bright A_FaceTarget
       SKLL B 4 Bright A_BruisAttack
       Goto See
   Pain:
       SKLL C 3
       SKLL C 3 A_Pain
       Goto See
   Death:
       SKLL D 6
       SKLL E 6 A_Scream
       SKLL F 6
       SKLL G 6 A_Noblocking
       SKLL H 6
       Stop
   } 
}

ACTOR HellSmith
{
  Health 8000 //Change: 20000 --> 8000 health for balance reasons
  Radius 40
  Height 110
  Mass 1000
  Speed 20
  PainChance 0
  Monster
  MinMissileChance 160
  +BOSS //Added
  +FLOORCLIP
  +DONTMORPH
  +BOSSDEATH
  Obituary "%o was incinerated by a Hell Smith."
  SeeSound "leviathan/sight"
  PainSound "brain/pain"
  DeathSound "leviathan/death"
  ActiveSound "cyber/active"
  Tag "Hell Smith"
  DamageFactor "Heroic", 3.0
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_BossCheck") == 3, "Reset")
	TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SURVIVE_IN_HELL, 30), "Reset")
    Goto Idle
  Idle:
    AVTR A 10 A_Look
    Loop
  Reset:
    TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
    TNT1 A 0 A_SpawnItemEx("Cyberdemon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
    Stop
  See:
    AVTR AB 6 A_Chase
    Loop
  Missile:
    AVTR C 3 Bright A_FaceTarget
    AVTR D 1 Bright A_FatAttack3
    AVTR D 1 Bright A_FatAttack2
    AVTR D 1 Bright A_FatAttack1
    AVTR E 1 Bright A_FatAttack1
    AVTR E 1 Bright A_FatAttack2
    AVTR E 1 Bright A_FatAttack3
    Goto See
  Pain:
    AVTR F 10 A_Pain
    Goto See
  Death:
    AVTR G 5 A_Scream
    AVTR HIJK 5 Bright
    AVTR L 5 Bright A_NoBlocking
    AVTR MNO 5 Bright
    AVTR P 75
    AVTR P -1 A_BossDeath
    Stop
  }
}

ACTOR SIHCybruiser
{ 
Health 1300 
Radius 24 
Height 64 
Mass 1000 
Speed 8 
PainChance 20 
MONSTER
MeleeDamage 10
Obituary "%o was blown away by a Cybruiser."
HitObituary "%o was smashed by a Cybruiser."
Tag "Cybruiser"
+FLOORCLIP
+NORADIUSDMG
SeeSound "monster/brusit" 
PainSound "baron/pain" 
DeathSound "monster/brudth"
MeleeSound "baron/melee"
DamageFactor "Blessed", 3.0
DamageFactor "Scrapper", 3.0
States 
   { 
   Spawn:
       TNT1 A 0 //This is only spawned by Icon of Sin, so don't level-scale it.
       TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("Dehacked_MonsterReset", DEH_SURVIVE_IN_HELL, 1), "Reset")
       Goto Idle
   Reset:
       TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
       TNT1 A 0 A_SpawnItemEx("HaymakerSummon", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERAMBUSHFLAG|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION, 0)
       Stop
   Idle:
       BRUS AB 10 A_Look 
       Loop 
   See:
       BRUS A 1 A_Playsound("monster/bruwlk")
       BRUS A 2 A_Chase
       BRUS A 3 A_Chase
       BRUS BB 3 A_Chase
       BRUS C 1 A_Playsound("monster/bruwlk")
       BRUS C 2 A_Chase
       BRUS C 3 A_Chase 
       BRUS DD 3 A_Chase
       Loop 
   Missile:
       BRUS E 4 Bright A_FaceTarget
       BRUS F 4 Bright A_CyberAttack
       BRUS E 4 A_FaceTarget
       BRUS E 4 Bright A_FaceTarget
       BRUS F 4 Bright A_CyberAttack
       Goto See
   Melee:
       BRUS GH 8 A_FaceTarget
       BRUS I 8 A_MeleeAttack
       Goto See
   Pain: 
       BRUS J 2 
       BRUS J 2 A_Pain 
       Goto See 
   Death: 
       BRUS K 6 Bright A_Scream
       BRUS LMN 6 Bright
       BRUS O 6 Bright A_NoBlocking
       BRUS QRS 6 Bright
       BRUS T -1
       Stop
   } 
}

ACTOR HellSmithFinal : HellSmith
{
  Health 20000
  +MISSILEMORE
  +MISSILEEVENMORE
  States
  {
  Spawn:
    AVTR A 10 A_Look
    Goto Idle
  Idle:
    AVTR A 10 A_Look
    Loop
  Pain:
    AVTR F 10 A_BrainPain
    Goto See
  Death:
    AVTR G 5 A_PlaySound("brain/death", 0, 1.0, 0, ATTN_NONE)
    AVTR HIJK 5 Bright
    AVTR L 5 Bright A_NoBlocking
    AVTR MNO 5 Bright
    AVTR P 75
    AVTR P -1 A_BrainDie
    Stop
  }
}